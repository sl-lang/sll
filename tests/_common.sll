(--- "json.sll" "file.sll" "sys.sll" "path.sll" "string.sll" "time.sll" "debug.sll" "array.sll")



(= _gh_actions (!! (: sys$ENVIRONMENT "GITHUB_ACTIONS")))
(= _test_data nil)
(= _test_results nil)



(= expect (,,, result value name
	(? (=== _test_results nil) {
		(:> "Function 'expect' executed outside of function 'run'!\n")
		(@@)
	})
	(? (=== result value) {
		(<- array$push _test_data$success name)
		(:> "+ " name "\n")
	} {
		(<- array$push _test_data$error name)
		(:> "- " name "\n")
		(= err (+ "Expected " (<- string$str value) ", got " (<- string$str result)))
		(? _gh_actions {
			(= loc (: (: (<- debug$get_call_stack) -1) "return"))
			(:> (<- string$format "::warning file=%s,line=%u,title=%s%3A %s::%s\n" loc$file loc$line (<- string$title_case sys$PLATFORM) (<- string$replace name "," "%2C") err))
		} {
			(:> "  " err "\n")
		})
	})
))



(= run (,,, name fn
	(? (!== _test_results nil) {
		(:> "Function 'run' executed more than once!\n")
		(@@)
	})
	(= _test_results <>)
	(= f (<- file$open "build/test_results.json" "r"))
	(? (!== f nil) {
		(= bf "")
		(>- (= chunk "") chunk
			(= chunk (<- file$read f 1024))
			(= bf (+ bf chunk))
		)
		(<- file$close f)
		(= _test_results (<- json$parse bf))
	})
	(= id (: (<- string$split (: (<- path$split (: sys$ARGV 0)) 1) ".") 0))
	(= _test_data <"name" name "success" [] "error" [] "time" 0>)
	(= (: _test_results id) _test_data)
	(? _gh_actions {
		(:> "::group::" name "\n")
	} {
		(:> ">>> " name "\n")
	})
	(= start_time (<- time$time))
	(<- fn)
	(? _gh_actions {
		(:> "::endgroup::\n")
	})
	(= _test_data$time (- (<- time$time) start_time))
	(= f (<- file$open "build/test_results.json" "w"))
	(<- file$write f (<- json$stringify _test_results))
	(<- file$close f)
))



(## expect run)
