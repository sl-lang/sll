(--- "process.sll" "path.sll" "sys.sll" "array.sll" "json.sll" "file.sll" "types.sll" "string.sll")



(= GH_ACTIONS (!! (: sys$ENVIRONMENT "GITHUB_ACTIONS")))



(= args [(?: (=== sys$PLATFORM "windows") "build\\sll.exe" "build/sll") "-I" "tests" "-I" "build/lib_ext"])
(= list (<- path$list_dir "tests"))
(-> (= i 0) (< i ($ list))
	(= name (: list i))
	(++ i)
	(? (=== (: name 0) '_') (<<<))
	(<- array$push args (+ "tests/" name))
)
(? (: (<- process$start args) "return_code") {
	(? GH_ACTIONS {
		(:> "::error file=tests/_runner.sll,title=Test runner::Child process crashed while executing test cases\n")
	})
	(:> "Child process crashed!\n")
	(@@ 1)
})
(= f (<- file$open "build/test_results.json" "r"))
(? (=== f nil) {
	(? GH_ACTIONS {
		(:> "::error file=tests/_runner.sll,title=Test runner::Test result file missing\n")
	})
	(:> "No test result file found!\n")
	(@@ 1)
})
(= bf "")
(>- (= chunk "") chunk
	(= chunk (<- file$read f 1024))
	(= bf (+ bf chunk))
)
(<- file$close f)
(= results (<- json$parse bf))
(? GH_ACTIONS {
	(:> "::group::Results\n")
} {
	(:> "===> Results <===\n")
})
(= success 0)
(= error 0)
(= data (:: results map_value_type))
(-> (= i 0) (< i ($ data))
	(= k (: data i))
	(= success (+ success k$success))
	(= error (+ error ($ k$error)))
	(++ i)
)
(= total (+ success error))
(:> (<- string$format "Total tests: %u\nPassed: %u (%u%%)\nFailed: %u (%u%%)\n"
	total
	success
	(// (* success 100) total)
	error
	(// (* error 100) total)
))
(? GH_ACTIONS {
	(:> "::endgroup::\n")
})
