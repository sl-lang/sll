(---
	"array.sll"
	"file.sll"
	"json.sll"
	"math.sll"
	"path.sll"
	"process.sll"
	"sort.sll"
	"string.sll"
	"sys.sll"
	"thread.sll"
	"types.sll"
)



(= GH_ACTIONS (!! (: sys$ENVIRONMENT "GITHUB_ACTIONS")))



(= build_command (,,,
	(= args [
		sys$EXECUTABLE
		"-I" "@test|tests"
		"-v"
	])
	(= file_list (<- path$list_dir "tests" true))
	(<- sort$sort file_list false true)
	(-> (= i 0) (< i ($ file_list))
		(= file_path (: file_list i))
		(++ i)
		(? (||
			(=== (: (: (<- path$split file_path) 1) 0) '_')
			(!== (>> file_path (<- string$index_reverse file_path '.')) ".sll")
		) (<<<))
		(<- array$push args file_path)
	)
	(@@ args)
))

(= read_results (,,, path
	(= fh (<- file$open path "r"))
	(? (=== fh nil) {
		(? GH_ACTIONS (:> "::error file=tests/_runner.sll,title=Test runner::Test result file missing\n"))
		(:> "No test result file found!\n")
		(<- thread$exit 1)
	})
	(@@ (<- json$parse (<- file$read fh)))
))

(= print_stats (,,, results
	(= success 0)
	(= error 0)
	(= data (:: results map_value_type))
	(-> (= i 0) (< i ($ data))
		(= k (: data i))
		(= success (+ success k$success))
		(= error (+ error ($ k$error)))
		(++ i)
	)
	(= total (+ success error))
	(= error_percent (<- math$ceil (/ (* error 100) total)))
	(:> (<- string$format "Total tests: %u\nPassed: %u (%u%%)\nFailed: %u (%u%%)\n"
		total
		success
		(- 100 error_percent)
		error
		error_percent
	))
))

(= generate_summary (,,, results out_path
	(= fh (<- file$open out_path "w"))
	(<- file$write fh
`<table>
	<tr>
		<th>Name</th>
		<th>File</th>
		<th>Passed</th>
		<th>Failed</th>
		<th>Total</th>
	</tr>
`)
	(= data (:: results map_value_type))
	(= passed 0)
	(= failed 0)
	(-> (= i 0) (< i ($ data))
		(= v (: data i))
		(= passed (+ passed v$success))
		(= failed (+ failed ($ v$error)))
		(<- file$write fh
`	<tr>
		<td>` v$name `</td>
		<td><code>` v$file `</code></td>
		<td>` v$success `</td>
		<td>` ($ v$error) `</td>
		<td>` (+ v$success ($ v$error)) `</td>
	</tr>
`)
		(++ i)
	)
	(<- file$write fh
`	<tr>
		<td colspan="2"><strong>Total</strong></td>
		<td>` passed `</td>
		<td>` failed `</td>
		<td>` (+ passed failed) `</td>
	</tr>
</table>
`)
	(<- file$close fh)
))



(? (<- path$exists "build/test_results.json")
	(<- file$delete "build/test_results.json")
)
(? (: (<- process$start (<- build_command)) "return_code") {
	(? GH_ACTIONS {
		(:> "::error file=tests/_runner.sll,title=Test runner::Child process crashed while executing test cases\n")
	})
	(:> "Child process crashed!\n")
	(@@ 1)
})
(= results (<- read_results "build/test_results.json"))
(? GH_ACTIONS {
	(:> "::group::Results\n")
} {
	(:> "===> Results <===\n")
})
(<- print_stats results)
(? GH_ACTIONS {
	(:> "::endgroup::\n")
	(<- generate_summary results (: sys$ENVIRONMENT "GITHUB_STEP_SUMMARY"))
})
