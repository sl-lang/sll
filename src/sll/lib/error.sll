(--- "types.sll" "string.sll" "array.sll")



(= call_frame_type (&:
	string_type file
	int_type line
	string_type func
))

(= error_type (&:
	(,,, err
		(= o (<- string$format "Error: %s" err$name))
		(-> (= i ($ err$backtrace)) i {
			(-- i)
			(= frame (: err$backtrace i))
			(= o (+ o (<- string$format "\nat %s:%u (%s)" frame$file frame$line frame$func)))
		})
		(@@ o)
	) @@string@@
	string_type name
	array_type backtrace
))

(= error_module_type (&:
	(~ type_type) error_type
	(~ func_type) peek
	(~ func_type) pop
	(~ func_type) push
))



(= _error_thread_data <>)



(= pop (,,,
	(= arr (: _error_thread_data (!.)))
	(? (|| (! arr) (! ($ arr))) (@@ nil))
	(@@ (<- array$pop arr))
))

(= peek (,,,
	(= arr (: _error_thread_data (!.)))
	(? (|| (! arr) (! ($ arr))) (@@ nil))
	(@@ (: arr -1))
))

(= push (,,, nm
	(= backtrace (<- (... "sll:error_get_backtrace")))
	(? (! (|: _error_thread_data (!.))) {
		(= (: _error_thread_data (!.)) [])
	})
	(<- array$push (: _error_thread_data (!.))
		(. error_type
			(:: nm string_type)
			([> (= i 0) (< i ($ backtrace))
				(= k (: backtrace i))
				(++ i)
				(:: k call_frame_type)
			)
		)
	)
))



(= error (. error_module_type
	error_type
	peek
	pop
	push
))
(## error)
