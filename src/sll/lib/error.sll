(--- "types.sll" "string.sll" "array.sll")



(= call_frame_type (&:
	(~ string_type) file
	(~ int_type) line
	(~ string_type) func
))

(= error_type (&:
	(,,, err
		(= o (<- string$format "%s" err$error_data))
		(-> (= i ($ err$backtrace)) i {
			(-- i)
			(= frame (: err$backtrace i))
			(= o (+ o (<- string$format "\nat %s:%u (%s)"
				frame$file
				frame$line
				frame$func
			)))
		})
		(@@ o)
	) @@string@@
	(~ object_type) error_data
	(~ array_type) backtrace
))

(= error_module_type (&:
	(~ type_type) error_type
	(~ type_type) ERROR_FILE_NOT_FOUND
	(~ type_type) ERROR_INVALID_BASE64_CHARACTER
	(~ type_type) ERROR_INVALID_BASE64_PADDING
	(~ type_type) ERROR_OUT_OF_RANGE
	(~ type_type) ERROR_UNKNOWN_TYPE
	(~ func_type) create
))



(= ERROR_FILE_NOT_FOUND (&:
	(,,, dt
		(@@ (<- string$format "Unable to open file '%s': File not found" dt$fp))
	) @@string@@
	(~ string_type) fp
))

(= ERROR_INVALID_BASE64_CHARACTER (&:
	(,,, dt
		(@@ (<- string$format "Invalid Base64 character at offset %llu: '%c'" dt$offset dt$character))
	) @@string@@
	(~ int_type) offset
	(~ char_type) character
))

(= ERROR_INVALID_BASE64_PADDING (&:
	(,,, dt
		(@@ "Invalid Base64 padding")
	) @@string@@
))

(= ERROR_OUT_OF_RANGE (&:
	(,,, dt
		(@@ (<- string$format "Value '%lld' is not between %lld and %lld" dt$v dt$min dt$max))
	) @@string@@
	(~ int_type) v
	(~ int_type) min
	(~ int_type) max
))

(= ERROR_UNKNOWN_TYPE (&:
	(,,, dt
		(@@ (<- string$format "Unknown type: '%c'" dt$type))
	) @@string@@
	(~ char_type) type
))



(= create (,,, type @@data@@
	(= backtrace (<- (... "sll:error_get_backtrace")))
	(? (=== type nil) (@@ nil))
	(= type (:: type type_type))
	(@@ (. error_type
		(:: @@data@@ type)
		([> (= i 0) (< i ($ backtrace))
			(= k (: backtrace i))
			(++ i)
			(:: k call_frame_type)
		)
	))
))



(= error (. error_module_type
	error_type
	ERROR_FILE_NOT_FOUND
	ERROR_INVALID_BASE64_CHARACTER
	ERROR_INVALID_BASE64_PADDING
	ERROR_OUT_OF_RANGE
	ERROR_UNKNOWN_TYPE
	create
))
(## error)
