(--- "types.sll" "sort.sll" "math.sll" "string.sll" "error.sll" "array.sll")



(# mean variance)



(= linear_regression_type (&:
	(,,, dt
		(@@ (<- string$format "y=x*%f+%f" dt$slope dt$intercept))
	) @@string@@
	float_type slope
	float_type intercept
))

(= statistics_module_type (&:
	(~ type_type) linear_regression_type
	(~ func_type) correlation
	(~ func_type) geometric_mean
	(~ func_type) harmonic_mean
	(~ func_type) linear_regression
	(~ func_type) mean
	(~ func_type) median
	(~ func_type) median_high
	(~ func_type) median_low
	(~ func_type) mode
	(~ func_type) quantiles
	(~ func_type) stddev
	(~ func_type) variance
))



(= _median_calc (,,, data
	(= data (:: data array_type))
	(= len ($ data))
	(? (! len) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(= data (<- sort$sort data false false))
	(= odd (& len 1))
	(= len (>> len 1))
	(@@ [
		(: data (- len (! odd)))
		(: data len)
	])
))

(= correlation (,,, x y
	(= x (:: x array_type))
	(= y (:: y array_type))
	(= len ($ x))
	(? (!== len ($ y)) (@@ (<- error$create ERROR_LENGTH_MISMATCH len ($ y))))
	(? (< len 2) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA len 2)))
	(= x_mean (<- mean x))
	(= y_mean (<- mean y))
	(= sum_xx 0)
	(= sum_xy 0)
	(= sum_yy 0)
	(>- (= i 0) (< i len)
		(= a (- (: x i) x_mean))
		(= b (- (: y i) y_mean))
		(= sum_xx (+
			sum_xx
			(* a a)
		))
		(= sum_xy (+
			sum_xy
			(* a b)
		))
		(= sum_yy (+
			sum_yy
			(* b b)
		))
		(++ i)
	)
	(= den (<- math$sqrt (* sum_xx sum_yy)))
	(? (! den) (@@ nil))
	(@@ (/ sum_xy den))
))

(= geometric_mean (,,, data
	(= data (:: data array_type))
	(? (! ($ data)) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(@@ (<- math$exp
		(<- mean ([> (= i 0) (< i ($ data))
			(= k (: data i))
			(++ i)
			(<- math$log k)
		))
	))
))

(= harmonic_mean (,,, data
	(= data (:: data array_type))
	(? (! ($ data)) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(= out 0)
	(-> (= i 0) (< i ($ data))
		(= out (+
			out
			(/ 1 (: data i))
		))
		(++ i)
	)
	(@@ (/ ($ data) out)
))

(= linear_regression (,,, x y
	(= x (:: x array_type))
	(= y (:: y array_type))
	(= len ($ x))
	(? (!== len ($ y)) (@@ (<- error$create ERROR_LENGTH_MISMATCH len ($ y))))
	(? (< len 2) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA len 2)))
	(= x_mean (<- mean x))
	(= y_mean (<- mean y))
	(= sum_xx 0)
	(= sum_xy 0)
	(>- (= i 0) (< i len)
		(= a (- (: x i) x_mean))
		(= b (- (: y i) y_mean))
		(= sum_xx (+
			sum_xx
			(* a a)
		))
		(= sum_xy (+
			sum_xy
			(* a b)
		))
		(++ i)
	)
	(? (! sum_xx) (@@ nil))
	(= slope (/ sum_xy sum_xx))
	(@@ (. linear_regression_type
		slope
		(- y_mean (* slope x_mean))
	))
))

(= mean (,,, data
	(= data (:: data array_type))
	(? (! ($ data)) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(= out 0)
	(-> (= i 0) (< i ($ data))
		(= out (+
			out
			(:: (: data i) float_type)
		))
		(++ i)
	)
	(@@ (/ out ($ data)))
))

(= median (,,, data
	(= out (<- _median_calc data))
	(@@ (?: (=== (:? out) array_type)
		(/ (+ (: out 0) (: out 1)) 2)
		out
	))
))

(= median_high (,,, data
	(= out (<- _median_calc data))
	(@@ (?: (=== (:? out) array_type)
		(: out 1)
		out
	))
))

(= median_low (,,, data
	(= out (<- _median_calc data))
	(@@ (?: (=== (:? out) array_type)
		(: out 0)
		out
	))
))

(= mode (,,, data
	(= data (:: data array_type))
	(? (! ($ data)) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(= out <>)
	(= max 0)
	(= max_v nil)
	(-> (= i 0) (< i ($ data))
		(= k  (: data i))
		(= (: out k) (+ (: out k) 1))
		(? (> (: out k) max) {
			(= max (: out k))
			(= max_v k)
		})
		(++ i)
	)
	(@@ max_v)
))

(= quantiles (,,, data count
	(= data (:: data array_type))
	(= count (?: count (:: count int_type) 4))
	(= len ($ data))
	(? (! len) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(= data (<- sort$sort data false false))
	(= out [])
	(-> (= i 1) (< i count)
		(= j (// (* i (+ len 1)) count))
		(?
			(< j 1) (= j 1)
			(> j (- len 1)) (= j (- len 1))
		)
		(= k (-
			(* i (+ len 1))
			(* j count)
		))
		(<- array$push out (/
			(+
				(* (: data (- j 1)) (- count k))
				(* (: data j) k)
			)
			count
		))
		(++ i)
	)
	(@@ out)
))

(= stddev (,,, data
	(= out (<- variance data))
	(@@ (?: (=== (:? out) float_type)
		(<- math$sqrt out)
		out
	))
))

(= variance (,,, data
	(= data (:: data array_type))
	(= len ($ data))
	(? (! len) (@@ (<- error$create ERROR_NOT_ENOUGH_DATA 0 1)))
	(= m (<- mean data))
	(? (!== (:? m) float_type) (@@ m))
	(= out 0)
	(-> (= i 0) (< i len)
		(= n (- (: data i) m))
		(= out (+
			out
			(* n n)
		))
		(++ i)
	)
	(@@ (/ out len))
))



(= statistics (. statistics_module_type
	linear_regression_type
	correlation
	geometric_mean
	harmonic_mean
	linear_regression
	mean
	median
	median_high
	median_low
	mode
	quantiles
	stddev
	variance
))
(## statistics)
