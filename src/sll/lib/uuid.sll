(--- "types.sll" "string.sll" "random.sll" "hash.sll")



(= uuid_type (&:
	(,,, dt {
		(@@ (<- string$format "%.8x-%.4x-%.4x-%.2x%.2x-%.12x"
			dt$time_low
			dt$time_mid
			dt$time_hi_and_version
			dt$clock_seq_hi_and_reserved
			dt$clock_seq_low
			dt$node
		))
	}) @@string@@
	int_type time_low
	int_type time_mid
	int_type time_hi_and_version
	int_type clock_seq_hi_and_reserved
	int_type clock_seq_low
	int_type node
))

(= uuid_module_type (&:
	(~ type_type) uuid_type
	(~ uuid_type) NAMESPACE_DNS
	(~ uuid_type) NAMESPACE_OID
	(~ uuid_type) NAMESPACE_URL
	(~ uuid_type) NAMESPACE_X500
	(~ uuid_type) NIL
	(~ func_type) from_bytes
	(~ func_type) to_bytes
	(~ func_type) uuid3
	(~ func_type) uuid4
	(~ func_type) uuid5
))



(= NIL (. uuid_type
	0
	0
	0
	0
	0
	0
))

(= NAMESPACE_DNS (. uuid_type
	0x6ba7b810
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))

(= NAMESPACE_OID (. uuid_type
	0x6ba7b812
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))

(= NAMESPACE_URL (. uuid_type
	0x6ba7b811
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))

(= NAMESPACE_X500 (. uuid_type
	0x6ba7b813
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))



(= from_bytes (,,, dt v
	(= dt (:: dt string_type))
	(? (< ($ dt) 16) {
		(= dt (+
			(* '\x00' (- 16 ($ dt)))
			dt
		))
	})
	(= o (. uuid_type
		(| (<< (: dt 0) 24) (<< (: dt 1) 16) (<< (: dt 2) 8) (: dt 3))
		(| (<< (: dt 4) 8) (: dt 5))
		(| (<< (: dt 6) 8) (: dt 7))
		(: dt 8)
		(: dt 9)
		(| (<< (: dt 10) 40) (<< (: dt 11) 32) (<< (: dt 12) 24) (<< (: dt 13) 16) (<< (: dt 14) 8) (: dt 15))
	))
	(= v (:: v int_type))
	(? (&& (> v 0) (< v 6)) {
		(= o$clock_seq_hi_and_reserved (| 0x80 (& o$clock_seq_hi_and_reserved 0x3f)))
		(= o$time_hi_and_version (| (<< v 12) (& o$time_hi_and_version 0xfff)))
	})
	(@@ o)
))

(= to_bytes (,,, dt
	(= dt (:: dt uuid_type))
	(@@ (+
		(:: (>> dt$time_low 24) char_type string_type)
		(:: (>> dt$time_low 16) char_type)
		(:: (>> dt$time_low 8) char_type)
		(:: dt$time_low char_type)
		(:: (>> dt$time_mid 8) char_type)
		(:: dt$time_mid char_type)
		(:: (>> dt$time_hi_and_version 8) char_type)
		(:: dt$time_hi_and_version char_type)
		(:: dt$clock_seq_hi_and_reserved char_type)
		(:: dt$clock_seq_low char_type)
		(:: (>> dt$node 40) char_type)
		(:: (>> dt$node 32) char_type)
		(:: (>> dt$node 24) char_type)
		(:: (>> dt$node 16) char_type)
		(:: (>> dt$node 8) char_type)
		(:: dt$node char_type)
	))
))

(= uuid3 (,,, ns nm
	(@@ (<- from_bytes
		(: (<- hash$md5$process (+
			(<- to_bytes (:: ns uuid_type))
			(:: nm string_type)
		)) 0 16)
		3
	))
))

(= uuid4 (,,,
	(@@ (<- from_bytes (<- random$get_string 16) 4))
))

(= uuid5 (,,, ns nm
	(@@ (<- from_bytes
		(: (<- hash$sha1$process (+
			(<- to_bytes (:: ns uuid_type))
			(:: nm string_type)
		)) 0 16)
		5
	))
))



(= uuid (. uuid_module_type
	uuid_type
	NAMESPACE_DNS
	NAMESPACE_OID
	NAMESPACE_URL
	NAMESPACE_X500
	NIL
	from_bytes
	to_bytes
	uuid3
	uuid4
	uuid5
))
(## uuid)
