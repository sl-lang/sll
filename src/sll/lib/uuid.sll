(---
	"hash.sll"
	"random.sll"
	"string.sll"
	"types.sll"
)



(= uuid_type (&:
	(,,, data
		(@@ (<- string$format "%.8x-%.4x-%.4x-%.2x%.2x-%.12x"
			data$time_low
			data$time_mid
			data$time_hi_and_version
			data$clock_seq_hi_and_reserved
			data$clock_seq_low
			data$node
		))
	) @@string@@
	int_type time_low
	int_type time_mid
	int_type time_hi_and_version
	int_type clock_seq_hi_and_reserved
	int_type clock_seq_low
	int_type node
))

(= uuid_module_type (&:
	(~ type_type) uuid_type
	(~ uuid_type) NAMESPACE_DNS
	(~ uuid_type) NAMESPACE_OID
	(~ uuid_type) NAMESPACE_URL
	(~ uuid_type) NAMESPACE_X500
	(~ uuid_type) NIL
	(~ func_type) from_bytes
	(~ func_type) to_bytes
	(~ func_type) uuid3
	(~ func_type) uuid4
	(~ func_type) uuid5
))



(= NIL (. uuid_type
	0
	0
	0
	0
	0
	0
))

(= NAMESPACE_DNS (. uuid_type
	0x6ba7b810
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))

(= NAMESPACE_OID (. uuid_type
	0x6ba7b812
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))

(= NAMESPACE_URL (. uuid_type
	0x6ba7b811
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))

(= NAMESPACE_X500 (. uuid_type
	0x6ba7b813
	0x9dad
	0x11d1
	0x80
	0xb4
	0x00c04fd430c8
))



(= from_bytes (,,, data version
	(= data (:: data string_type))
	(? (< ($ data) 16) {
		(= data (+
			(* '\x00' (- 16 ($ data)))
			data
		))
	})
	(= out (. uuid_type
		(| (<< (: data 0) 24) (<< (: data 1) 16) (<< (: data 2) 8) (: data 3))
		(| (<< (: data 4) 8) (: data 5))
		(| (<< (: data 6) 8) (: data 7))
		(: data 8)
		(: data 9)
		(| (<< (: data 10) 40) (<< (: data 11) 32) (<< (: data 12) 24) (<< (: data 13) 16) (<< (: data 14) 8) (: data 15))
	))
	(= version (:: version int_type))
	(? (&& (> version 0) (< version 6)) {
		(= out$clock_seq_hi_and_reserved (| 0x80 (& out$clock_seq_hi_and_reserved 0x3f)))
		(= out$time_hi_and_version (| (<< version 12) (& out$time_hi_and_version 0xfff)))
	})
	(@@ out)
))

(= to_bytes (,,, data
	(= data (:: data uuid_type))
	(@@ (+
		(:: (>> data$time_low 24) char_type string_type)
		(:: (>> data$time_low 16) char_type)
		(:: (>> data$time_low 8) char_type)
		(:: data$time_low char_type)
		(:: (>> data$time_mid 8) char_type)
		(:: data$time_mid char_type)
		(:: (>> data$time_hi_and_version 8) char_type)
		(:: data$time_hi_and_version char_type)
		(:: data$clock_seq_hi_and_reserved char_type)
		(:: data$clock_seq_low char_type)
		(:: (>> data$node 40) char_type)
		(:: (>> data$node 32) char_type)
		(:: (>> data$node 24) char_type)
		(:: (>> data$node 16) char_type)
		(:: (>> data$node 8) char_type)
		(:: data$node char_type)
	))
))

(= uuid3 (,,, namespace name
	(@@ (<- from_bytes
		(: (<- hash$md5$process (+
			(<- to_bytes (:: namespace uuid_type))
			(:: name string_type)
		)) 0 16)
		3
	))
))

(= uuid4 (,,,
	(@@ (<- from_bytes (<- random$get_string 16) 4))
))

(= uuid5 (,,, namespace name
	(@@ (<- from_bytes
		(: (<- hash$sha1$process (+
			(<- to_bytes (:: namespace uuid_type))
			(:: name string_type)
		)) 0 16)
		5
	))
))



(= uuid (. uuid_module_type
	uuid_type
	NAMESPACE_DNS
	NAMESPACE_OID
	NAMESPACE_URL
	NAMESPACE_X500
	NIL
	from_bytes
	to_bytes
	uuid3
	uuid4
	uuid5
))
(## uuid)
