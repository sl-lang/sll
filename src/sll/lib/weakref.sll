(---
	"string.sll"
	"types.sll"
)



(# get)



(= weakref_type (&:
	(,,, data
		(<- (... "sll:weakref_delete") data$id)
	) @@delete@@
	(,,, data
		(@@ (<- string$str (<- get data)))
	) @@string@@
	(~ int_type) _id
	(~ array_type) callbacks
))

(= weakref_no_object (&:
	(,,, (@@ "<WEAKREF_NO_OBJECT>")) @@string@@
))

(= weakref_module_type (&:
	(~ type_type) weakref_type
	(~ weakref_no_object) WEAKREF_NO_OBJECT
	(~ func_type) get
	(~ func_type) ref
))



(= WEAKREF_NO_OBJECT (. weakref_no_object))



(= _execute_callbacks (,,, wr obj
	(-> (= i 0) (< i ($ wr$callbacks))
		(<- (: wr$callbacks i) obj wr)
		(++ i)
	)
))

(= get (,,, wr
	(? (=== wr nil) (@@ WEAKREF_NO_OBJECT))
	(= wr (:: wr weakref_type))
	(? (! wr$_id) (@@ WEAKREF_NO_OBJECT))
	(= out (<- (... "sll:weakref_get") wr$_id))
	(? (=== out WEAKREF_NO_OBJECT)
		(= wr$_id nil)
	)
	(@@ out)
))

(= ref (,,, obj
	(= out (. weakref_type
		(<- (... "sll:weakref_create") obj)
		[]
	))
	(<- (... "sll:weakref_set_callback_data") out$_id out)
	(@@ out)
))



(<- (... "sll:weakref__init") WEAKREF_NO_OBJECT _execute_callbacks)



(= weakref (. weakref_module_type
	weakref_type
	WEAKREF_NO_OBJECT
	get
	ref
))
(## weakref)
