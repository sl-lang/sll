(--- "types.sll" "string.sll")



(# get)



(= weakref_type (&:
	(,,, dt
		(<- (... "sll:weakref_delete") dt$id)
	) @@delete@@
	(,,, dt
		(@@ (<- string$str (<- get dt)))
	) @@string@@
	(~ int_type) _id
	(~ array_type) callbacks
))

(= no_object (&:
	(,,, (@@ "<WEAKREF_NO_OBJECT>")) @@string@@
))

(= weakref_module_type (&:
	(~ type_type) weakref_type
	(~ no_object) NO_OBJECT
	(~ func_type) get
	(~ func_type) ref
))



(= NO_OBJECT (. no_object))



(= _execute_callbacks (,,, wr obj
	(-> (= i 0) (< i ($ wr$callbacks))
		(<- (: wr$callbacks i) obj wr)
		(++ i)
	)
))

(= get (,,, wr
	(? (=== wr nil) (@@ NO_OBJECT))
	(= wr (:: wr weakref_type))
	(? (! wr$_id) (@@ NO_OBJECT))
	(= o (<- (... "sll:weakref_get") wr$_id))
	(? (=== o NO_OBJECT) (= wr$_id nil))
	(@@ o)
))

(= ref (,,, obj
	(= o (. weakref_type
		(<- (... "sll:weakref_create") obj)
		[]
	))
	(<- (... "sll:weakref_set_callback_data") o$_id o)
	(@@ o)
))



(<- (... "sll:weakref__init") NO_OBJECT _execute_callbacks)



(= weakref (. weakref_module_type
	weakref_type
	NO_OBJECT
	get
	ref
))
(## weakref)
