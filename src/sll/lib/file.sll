(--- "types.sll" "string.sll" "error.sll")



(# FLAG_READ FLAG_WRITE FLAG_APPEND FLAG_NO_BUFFER)



(= file_type (&:
	(,,, dt {
		(<- (... "sll:file_inc_handle") dt$_handle)
	}) @@copy@@
	(,,, dt {
		(? (=== f$_handle -1) (@@))
		(<- (... "sll:file_close") dt$_handle)
	}) @@delete@@
	(,,, dt {
		(= m "")
		(? (& dt$flags FLAG_APPEND) (= m (+ m "A")))
		(? (& dt$flags FLAG_NO_BUFFER) (= m (+ m "B")))
		(? (& dt$flags FLAG_READ) (= m (+ m "R")))
		(? (& dt$flags FLAG_WRITE) (= m (+ m "W")))
		(@@ (<- string$format "<path=%s, mode=%s>" dt$name m))
	}) @@string@@
	(~ string_type) name
	(~ int_type) flags
	int_type _handle
))

(= file_module_type (&:
	(~ type_type) file_type
	(~ type_type) ERROR_FILE_NOT_FOUND
	(~ int_type) FLAG_APPEND
	(~ int_type) FLAG_NO_BUFFER
	(~ int_type) FLAG_READ
	(~ int_type) FLAG_WRITE
	(~ string_type) TEMP_FILE_PATH
	(~ file_type) stdin
	(~ file_type) stdout
	(~ file_type) stderr
	(~ func_type) close
	(~ func_type) copy
	(~ func_type) delete
	(~ func_type) flush
	(~ func_type) from_data
	(~ func_type) get_buffer
	(~ func_type) open
	(~ func_type) peek
	(~ func_type) read
	(~ func_type) rename
	(~ func_type) write
))



(= ERROR_FILE_NOT_FOUND (&:))

(= FLAG_READ 1)
(= FLAG_WRITE 2)
(= FLAG_APPEND 4)
(= FLAG_NO_BUFFER 8)

(= TEMP_FILE_PATH (<- (... "sll:file_get_temp_path")))

(= stdin (. file_type
	"@stdin"
	(| FLAG_READ FLAG_NO_BUFFER)
	(<- (... "sll:file_std_handle") 0)
))
(= stdout (. file_type
	"@stdout"
	FLAG_WRITE
	(<- (... "sll:file_std_handle") 1)
))
(= stderr (. file_type
	"@stderr"
	FLAG_WRITE
	(<- (... "sll:file_std_handle") 2)
))



(= close (,,, f
	(? (=== f nil) (@@ nil))
	(= f (:: f file_type))
	(? (! (<- (... "sll:file_close") f$_handle)) (@@))
	(= f$_handle -1)
))

(= copy (,,, s d
	(? (|| (! s) (! d)) (@@ nil))
	(= s (:: s string_type))
	(= d (:: d string_type))
	(@@ (<- (... "sll:file_copy") s d))
))

(= delete (,,, fp
	(? (! fp) (@@ nil))
	(= fp (:: fp string_type))
	(@@ (<- (... "sll:file_delete") fp))
))

(= flush (,,, f
	(? (=== f nil) (@@ nil))
	(= f (:: f file_type))
	(<- (... "sll:file_flush") f$_handle)
))

(= from_data (,,, bf fl
	(= bf (?: (=== bf nil) "" (:: bf string_type)))
	(= fl (?: (=== fl nil) FLAG_READ (:: fl int_type)))
	(= handle (<- (... "sll:file_from_data") bf fl))
	(? (=== handle -1) (@@ nil))
	(@@ (. file_type
		"@data"
		fl
		handle
	))
))

(= get_buffer (,,, f
	(? (=== f nil) (@@ ""))
	(= f (:: f file_type))
	(? (! (& f$flags FLAG_WRITE)) (@@ ""))
	(@@ (<- (... "sll:file_get_buffer") f$_handle))
))

(= open (,,, path mode
	(? (=== path nil) (@@ nil))
	(= path (:: path string_type))
	(= flags FLAG_READ)
	(? (!== mode nil) {
		(= mode (<- string$lower_case (:: mode string_type)))
		(-> (= i 0) (< i ($ mode)) {
			(?? (: mode i)
				'a' (= flags (| FLAG_WRITE FLAG_APPEND))
				'r' (= flags FLAG_READ)
				'w' (= flags FLAG_WRITE)
			)
			(++ i)
		})
	})
	(= handle (<- (... "sll:file_open")
		path
		flags
	))
	(? (=== handle -1) {
		(@@ (<- error$create FILE_NOT_FOUND path (<- string$format "Unable to open file '%s'" path)))
	})
	(@@ (. file_type
		path
		flags
		handle
	))
))

(= peek (,,, f
	(? (=== f nil) (@@ nil))
	(= f (:: f file_type))
	(@@ (<- (... "sll:file_peek") f$_handle))
))

(= read (,,, f c
	(? (=== f nil) (@@ nil))
	(= f (:: f file_type))
	(= c (:: c int_type))
	(? (< c 0) {
		(@@ "")
	})
	(@@ (<- (... "sll:file_read") f$_handle c))
))

(= rename (,,, s d
	(? (|| (! s) (! d)) (@@ nil))
	(= s (:: s string_type))
	(= d (:: d string_type))
	(@@ (<- (... "sll:file_rename") s d))
))

(= write (,,, f @@dt@@
	(? (=== f nil) (@@ nil))
	(= f (:: f file_type))
	(= o 0)
	(-> (= i 0) (< i ($ @@dt@@))
		(= o (+ o (<- (... "sll:file_write")
			f$_handle
			(:: (: @@dt@@ i) string_type)
		)))
		(++ i)
	)
	(@@ o)
))



(= file (. file_module_type
	file_type
	ERROR_FILE_NOT_FOUND
	FLAG_APPEND
	FLAG_NO_BUFFER
	FLAG_READ
	FLAG_WRITE
	TEMP_FILE_PATH
	stdin
	stdout
	stderr
	close
	copy
	delete
	flush
	from_data
	get_buffer
	open
	peek
	read
	rename
	write
))
(## file)
