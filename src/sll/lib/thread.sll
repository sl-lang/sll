(--- "types.sll" "string.sll")



(# STATE_RUNNING STATE_WAITING_THREAD STATE_WAITING_LOCK STATE_WAITING_SEMAPHORE STATE_WAITING_BARRIER STATE_TERMINATED _thread_map)



(= barrier_type (&:
	(,,, dt
		(@@ (<- string$format "<barrier-%u counter=%u>" dt$id dt$counter))
	) @@string@@
	(~ int_type) id
	int_type counter
))

(= lock_type (&:
	(,,, dt
		(= state (?: (=== dt$blocking_thread -1)
			"unlocked"
			(+ "locked blocking_thread=" (<- string$str (: _thread_map dt$blocking_thread)))
		))
		(@@ (<- string$format "<lock-%u state=%s>" dt$id state))
	) @@string@@
	(~ int_type) id
	int_type blocking_thread
))

(= semaphore_type (&:
	(,,, dt
		(@@ (<- string$format "<semaphore-%u counter=%u/%u>" dt$id dt$counter dt$max))
	) @@string@@
	(~ int_type) id
	(~ int_type) max
	int_type counter
))

(= thread_type (&:
	(,,, dt
		(= state nil)
		(?? dt$state
			STATE_RUNNING (= state "running")
			STATE_WAITING_THREAD (= state (+ "waiting blocking=" (<- string$str (: _thread_map dt$blocking))))
			STATE_WAITING_LOCK (= state (+ "waiting blocking=" (<- string$str dt$blocking)))
			STATE_WAITING_SEMAPHORE (= state (+ "waiting blocking=" (<- string$str dt$blocking)))
			STATE_WAITING_BARRIER (= state (+ "waiting blocking=" (<- string$str dt$blocking)))
			(= state (+ "terminated return_value=" (<- string$str dt$return_value)))
		)
		(? (!== dt$suspended -1) (= state (+
			state
			" suspended="
			(<- string$str (: _thread_map dt$suspended))
		)))
		(@@ (<- string$format "<thread-%u state=%s>" dt$id state))
	) @@string@@
	(~ int_type) id
	int_type state
	object_type suspended
	object_type blocking
	object_type return_value
))

(= internal_thread_data_type (&:
	int_type instruction_index
	int_type stack_index
))

(= thread_module_type (&:
	(~ type_type) barrier_type
	(~ type_type) internal_thread_data_type
	(~ type_type) lock_type
	(~ type_type) semaphore_type
	(~ type_type) thread_type
	(~ int_type) STATE_RUNNING
	(~ int_type) STATE_TERMINATED
	(~ int_type) STATE_WAITING_BARRIER
	(~ int_type) STATE_WAITING_LOCK
	(~ int_type) STATE_WAITING_SEMAPHORE
	(~ int_type) STATE_WAITING_THREAD
	(~ func_type) acquire_lock
	(~ func_type) acquire_semaphore
	(~ func_type) create_barrier
	(~ func_type) create_lock
	(~ func_type) create_semaphore
	(~ func_type) currenct
	(~ func_type) get
	(~ func_type) get_internal_data
	(~ func_type) if_equal
	(~ func_type) if_greater_equal
	(~ func_type) increase_barrier
	(~ func_type) join
	(~ func_type) release_lock
	(~ func_type) release_semaphore
	(~ func_type) reset_barrier
	(~ func_type) start
	(~ func_type) suspend
))



(= STATE_RUNNING 0)
(= STATE_SUSPENDED 1)
(= STATE_WAITING_THREAD 2)
(= STATE_WAITING_LOCK 3)
(= STATE_WAITING_SEMAPHORE 4)
(= STATE_WAITING_BARRIER 5)
(= STATE_TERMINATED 6)



(= _thread_map <0 (. thread_type
	0
	STATE_RUNNING
	-1
	nil
	nil
)>)



(= _thread_runner (,,, fn args
	(= o (<-* fn args))
	(= thr nil)
	(>- (= id (!.)) (=== thr nil)
		(= thr (: _thread_map id))
	)
	(= thr$return_value o)
	(= thr$state STATE_TERMINATED)
	(@@ o)
))

(= acquire_lock (,,, lck
	(? (=== lck nil) (@@))
	(= lck (:: lck lock_type))
	(= thr (: _thread_map (!.)))
	(= thr$blocking lck)
	(= thr$state STATE_WAITING_LOCK)
	(!<* lck$id)
	(= thr$state STATE_RUNNING)
	(= lck$blocking_thread (!.))
))

(= acquire_semaphore (,,, sem
	(? (=== sem nil) (@@))
	(= sem (:: sem semaphore_type))
	(= thr (: _thread_map (!.)))
	(= thr$blocking sem)
	(= thr$state STATE_WAITING_SEMAPHORE)
	(!<+ sem$id)
	(= sem$counter (- sem$counter 1))
	(= thr$state STATE_RUNNING)
))

(= create_barrier (,,,
	(@@ (. lock_type
		(<- (... "sll:thread_create_barrier"))
		0
	))
))

(= create_lock (,,,
	(@@ (. lock_type
		(<- (... "sll:thread_create_lock"))
		-1
	))
))

(= create_semaphore (,,, c
	(= c (:: c int_type))
	(? (<= c 0) (@@ nil))
	(@@ (. semaphore_type
		(<- (... "sll:thread_create_semaphore") c)
		c
		c
	))
))

(= current (,,,
	(@@ (: _thread_map (!.)))
))

(= get (,,, id
	(@@ (: _thread_map id))
))

(= get_internal_data (,,, thr
	(? (=== thr nil) (@@ nil))
	(= thr (:: thr thread_type))
	(? (=== thr$state STATE_TERMINATED) (@@ nil))
	(@@ (:: (<- (... "sll:thread_get_internal_data") thr$id) internal_thread_data_type))
))

(= if_equal (,,, bar v
	(? (=== bar nil) (@@))
	(= bar (:: bar barrier_type))
	(= thr (: _thread_map (!.)))
	(= thr$blocking bar)
	(= thr$state STATE_WAITING_BARRIER)
	(!<= bar$id (:: v int_type))
	(= thr$state STATE_RUNNING)
))

(= if_greater_equal (,,, bar v
	(? (=== bar nil) (@@))
	(= bar (:: bar barrier_type))
	(= thr (: _thread_map (!.)))
	(= thr$blocking bar)
	(= thr$state STATE_WAITING_BARRIER)
	(!<> bar$id (:: v int_type))
	(= thr$state STATE_RUNNING)
))

(= increase_barrier (,,, bar
	(? (=== bar nil) (@@))
	(= bar (:: bar barrier_type))
	(= bar$counter (<- (... "sll:thread_increase_barrier") bar$id))
))

(= join (,,, thr
	(? (=== thr nil) (@@))
	(= thr (:: thr thread_type))
	(? (=== thr$state STATE_TERMINATED) (@@))
	(= c_thr (: _thread_map (!.)))
	(= c_thr$blocking thr$id)
	(= c_thr$state STATE_WAITING_THREAD)
	(!<< thr$id)
	(= c_thr$state STATE_RUNNING)
))

(= release_lock (,,, lck
	(? (=== lck nil) (@@))
	(= lck (:: lck lock_type))
	(? (=== lck$blocking_thread -1) (@@))
	(= lck$blocking_thread -1)
	(@@ (<- (... "sll:thread_release_lock") lck$id))
))

(= release_semaphore (,,, sem
	(? (=== sem nil) (@@))
	(= sem (:: sem semaphore_type))
	(? (=== sem$counter sem$max) (@@))
	(= sem$counter (+ sem$counter 1))
	(@@ (<- (... "sll:thread_release_semaphore") sem$id))
))

(= reset_barrier (,,, bar
	(? (=== bar nil) (@@))
	(= bar (:: bar barrier_type))
	(= bar$counter 0)
	(@@ (<- (... "sll:thread_reset_barrier") bar$id))
))

(= start (,,, fn @@args@@
	(?
		(=== fn nil) (@@ nil)
		(=== (:? fn) thread_type) {
			(? (=== fn$suspended -1) (@@ nil))
			(= fn$suspended -1)
			(<- (... "sll:thread_restart") fn$id)
			(@@ 1)
		}
	)
	(= tid (<- (... "sll:thread_create")
		_thread_runner
		[
			(:: fn func_type)
			@@args@@
		]
	))
	(= o (. thread_type
		tid
		STATE_RUNNING
		-1
		nil
		nil
	))
	(= (: _thread_map tid) o)
	(@@ o)
))

(= suspend (,,, thr
	(? (=== thr nil) (@@))
	(= thr (:: thr thread_type))
	(? (=== thr$state STATE_TERMINATED) (@@))
	(<- (... "sll:thread_suspend") thr$id)
	(= thr$suspended (!.))
))



(= thread (. thread_module_type
	barrier_type
	internal_thread_data_type
	lock_type
	semaphore_type
	thread_type
	STATE_RUNNING
	STATE_TERMINATED
	STATE_WAITING_BARRIER
	STATE_WAITING_LOCK
	STATE_WAITING_SEMAPHORE
	STATE_WAITING_THREAD
	acquire_lock
	acquire_semaphore
	create_barrier
	create_lock
	create_semaphore
	current
	get
	get_internal_data
	if_equal
	if_greater_equal
	increase_barrier
	join
	release_lock
	release_semaphore
	reset_barrier
	start
	suspend
))
(## thread)
