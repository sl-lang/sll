(--- "types.sll" "math.sll")



(## gauss2)



(= random_module_type (&:
	(~ func_type) choice
	(~ func_type) gauss
	(~ func_type) gauss2
	(~ func_type) get_char
	(~ func_type) get_float
	(~ func_type) get_int
	(~ func_type) get_string
	(~ func_type) shuffle
	(~ func_type) triangular
))



(= _next_gauss nil)



(= choice (,,, data
	(?? (:? data)
		int_type (@@ (<- (... "sll:random_get_int") 0 (- data 1)))
		float_type (@@ (<- (... "sll:get_float") 0 data))
		char_type (@@ (: data (<- (... "sll:random_get_int") 0 (- data 1))))
		string_type (@@ (: data (<- (... "sll:random_get_int") 0 (- ($ data) 1))))
		map_type (@@ (: (:: data map_key_type) (<- (... "sll:random_get_int") 0 (- ($ data) 1))))
		{
			(= data (:: data array_type))
			(@@ (: data (<- (... "sll:random_get_int") 0 (- ($ data) 1))))
		}
	)
))

(= gauss (,,, mu sigma
	(= o _next_gauss)
	(? (!== o nil) {
		(= _next_gauss nil)
		(@@ o)
	} {
		(= dt (<- gauss2))
		(= _next_gauss (: dt 0))
		(= o (: dt 1))
	})
	(@@ (+ mu (* o sigma)))
))

(= gauss2 (,,,
	(= u (<- math$sqrt (* -2 (<- math$log (<- (... "sll:random_get_float") 0 1)))))
	(= v (* (<- (... "sll:random_get_float") 0 1) math$TAU))
	(@@ [
		(* u (<- math$sin v))
		(* u (<- math$cos v))
	])
))

(= get_char (,,, mn mx {
	(= mn (:: mn char_type))
	(= mx (:: mx char_type))
	(? (=== mn mx) {
		(= mn 0)
		(= mx 255)
	})
	(@@ (:: (<- (... "sll:random_get_int")
		mn
		mx
	) char_type))
}))

(= get_float (,,, mn mx {
	(= mn (:: mn float_type))
	(= mx (:: mx float_type))
	(@@ (<- (... "sll:random_get_float")
		mn
		(?: (=== mn mx) (+ mx 1) mx)
	))
}))

(= get_int (,,, mn mx {
	(= mn (:: mn int_type))
	(= mx (:: mx int_type))
	(@@ (<- (... "sll:random_get_int")
		mn
		(?: (=== mn mx) (+ mx 1) mx)
	))
}))

(= get_string (,,, l mn mx {
	(= l (:: l int_type))
	(? (< l 1) {
		(@@ "")
	})
	(= mn (:: mn char_type))
	(= mx (:: mx char_type))
	(? (< mx mn) {
		(= mx 255)
	} (=== mn mx) {
		(? mn {
			(@@ (* (:: mn string_type) l))
		} {
			(= mx 255)
		})
	})
	(@@ (<- (... "sll:random_get_string")
		l
		mn
		mx
	))
}))

(= shuffle (,,, arr
	(? (|| (!== (:? arr) array_type) (< ($ arr) 2)) (@@))
	(-> (= i ($ arr)) (> i 1)
		(-- i)
		(= j (<- (... "sll:random_get_int") 0 i))
		(= tmp (: arr i))
		(= (: arr i) (: arr j))
		(= (: arr j) tmp)
	)
))

(= triangular (,,, a b c
	(? (=== a b) (@@ a))
	(= u (<- (... "sll:random_get_float") 0 1))
	(@@ (?: (< u (/ (- c a) (- b a)))
		(+ a (<- math$sqrt (* u (- b a) (- c a))))
		(- b (<- math$sqrt (* (- 1 u) (- b a) (- b c))))
	))
))



(= random (. random_module_type
	choice
	gauss
	gauss2
	get_char
	get_float
	get_int
	get_string
	shuffle
	triangular
))
(## random)
