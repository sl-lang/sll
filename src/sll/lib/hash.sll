(--- "types.sll")



(= md5_type (&:
	string_type _bf
	int_type _sz
	int_type _a
	int_type _b
	int_type _c
	int_type _d
))

(= sha1_type (&:
	string_type _bf
	int_type _sz
	int_type _a
	int_type _b
	int_type _c
	int_type _d
	int_type _e
))

(= sha256_type (&:
	string_type _bf
	int_type _sz
	int_type _a
	int_type _b
	int_type _c
	int_type _d
	int_type _e
	int_type _f
	int_type _g
	int_type _h
))

(= sha512_type (&:
	string_type _bf
	int_type _sz
	int_type _a
	int_type _b
	int_type _c
	int_type _d
	int_type _e
	int_type _f
	int_type _g
	int_type _h
))

(= hash_type (&:
	(~ int_type) BLOCK_SIZE
	(~ int_type) DIGEST_SIZE
	(~ func_type) init
	(~ func_type) update
	(~ func_type) digest
	(~ func_type) process
))

(= hash_module_type (&:
	(~ hash_type) md5
	(~ hash_type) sha1
	(~ hash_type) sha224
	(~ hash_type) sha256
	(~ hash_type) sha384
	(~ hash_type) sha512
	(~ func_type) hexdigest
))



(= MD5_BLOCK_SIZE 64)
(= MD5_DIGEST_SIZE 16)

(= SHA1_BLOCK_SIZE 64)
(= SHA1_DIGEST_SIZE 20)

(= SHA224_BLOCK_SIZE 64)
(= SHA224_DIGEST_SIZE 28)

(= SHA256_BLOCK_SIZE 64)
(= SHA256_DIGEST_SIZE 32)

(= SHA384_BLOCK_SIZE 128)
(= SHA384_DIGEST_SIZE 48)

(= SHA512_BLOCK_SIZE 128)
(= SHA512_DIGEST_SIZE 64)



(= md5_init (,,,
	(@@ (. md5_type
		""
		0
		0x67452301
		0xefcdab89
		0x98badcfe
		0x10325476
	))
))

(= md5_update (,,, md5 dt
	(= md5 (:: md5 md5_type))
	(? (=== dt nil)
		(@@ (: md5))
	)
	(= dt (:: dt string_type))
	(= len ($ dt))
	(? (! len)
		(@@ (: md5))
	)
	(= n_sz (+ md5$_sz len))
	(= dt (+ md5$_bf dt))
	(= len (+ len ($ md5$_bf)))
	(= i (& len 0xffffffffffffffc0))
	(? (! i) {
		(= o (: md5))
		(= o$_bf dt)
		(= o$_sz n_sz)
		(@@ o)
	})
	(= new_data (<- (... "sll:hash_md5")
		md5$_a
		md5$_b
		md5$_c
		md5$_d
		(: dt 0 i)
	))
	(@@ (. md5_type
		(: dt i len)
		n_sz
		(: new_data 0)
		(: new_data 1)
		(: new_data 2)
		(: new_data 3)
	))
))

(= md5_digest (,,, md5
	(= md5 (:: md5 md5_type))
	(= extra md5$_bf)
	(= len ($ extra))
	(= padding (- (?: (< len 56) 55 119) len))
	(= n (<< md5$_sz 3))
	(= extra (+
		extra
		'\x80'
		(* "\x00" padding)
		(:: n char_type)
		(:: (>> n 8) char_type)
		(:: (>> n 16) char_type)
		(:: (>> n 24) char_type)
		(:: (>> n 32) char_type)
		(:: (>> n 40) char_type)
		(:: (>> n 48) char_type)
		(:: (>> n 56) char_type)
	))
	(= md5_data (<- (... "sll:hash_md5")
		md5$_a
		md5$_b
		md5$_c
		md5$_d
		extra
	))
	(= o "")
	(-> (= i 0) (< i 4) {
		(= n (: md5_data i))
		(= o (+ o
			(:: n char_type)
			(:: (>> n 8) char_type)
			(:: (>> n 16) char_type)
			(:: (>> n 24) char_type)
		))
		(++ i)
	})
	(@@ o)
))

(= md5_process (,,, dt
	(@@ (<- md5_digest (<- md5_update (<- md5_init) dt)))
))



(= sha1_init (,,,
	(@@ (. sha1_type
		""
		0
		0x67452301
		0xefcdab89
		0x98badcfe
		0x10325476
		0xc3d2e1f0
	))
))

(= sha1_update (,,, sha1 dt
	(= sha1 (:: sha1 sha1_type))
	(? (=== dt nil)
		(@@ (: sha1))
	)
	(= dt (:: dt string_type))
	(= len ($ dt))
	(? (! len)
		(@@ (: sha1))
	)
	(= n_sz (+ sha1$_sz len))
	(= dt (+ sha1$_bf dt))
	(= len (+ len ($ sha1$_bf)))
	(= i (& len 0xffffffffffffffc0))
	(? (! i) {
		(= o (: sha1))
		(= o$_bf dt)
		(= o$_sz n_sz)
		(@@ o)
	})
	(= new_data (<- (... "sll:hash_sha1")
		sha1$_a
		sha1$_b
		sha1$_c
		sha1$_d
		sha1$_e
		(: dt 0 i)
	))
	(@@ (. sha1_type
		(: dt i len)
		n_sz
		(: new_data 0)
		(: new_data 1)
		(: new_data 2)
		(: new_data 3)
		(: new_data 4)
	))
))

(= sha1_digest (,,, sha1
	(= sha1 (:: sha1 sha1_type))
	(= extra sha1$_bf)
	(= len ($ extra))
	(= padding (- (?: (< len 56) 55 119) len))
	(= n (<< sha1$_sz 3))
	(= extra (+
		extra
		'\x80'
		(* "\x00" padding)
		(:: (>> n 56) char_type)
		(:: (>> n 48) char_type)
		(:: (>> n 40) char_type)
		(:: (>> n 32) char_type)
		(:: (>> n 24) char_type)
		(:: (>> n 16) char_type)
		(:: (>> n 8) char_type)
		(:: n char_type)
	))
	(= sha1_data (<- (... "sll:hash_sha1")
		sha1$_a
		sha1$_b
		sha1$_c
		sha1$_d
		sha1$_e
		extra
	))
	(= o "")
	(-> (= i 0) (< i 5) {
		(= n (: sha1_data i))
		(= o (+ o
			(:: (>> n 24) char_type)
			(:: (>> n 16) char_type)
			(:: (>> n 8) char_type)
			(:: n char_type)
		))
		(++ i)
	})
	(@@ o)
))

(= sha1_process (,,, dt
	(@@ (<- sha1_digest (<- sha1_update (<- sha1_init) dt)))
))



(= sha256_init (,,,
	(@@ (. sha256_type
		""
		0
		0x6a09e667
		0xbb67ae85
		0x3c6ef372
		0xa54ff53a
		0x510e527f
		0x9b05688c
		0x1f83d9ab
		0x5be0cd19
	))
))

(= sha256_update (,,, sha256 dt
	(= sha256 (:: sha256 sha256_type))
	(? (=== dt nil)
		(@@ (: sha256))
	)
	(= dt (:: dt string_type))
	(= len ($ dt))
	(? (! len)
		(@@ (: sha256))
	)
	(= n_sz (+ sha256$_sz len))
	(= dt (+ sha256$_bf dt))
	(= len (+ len ($ sha256$_bf)))
	(= i (& len 0xffffffffffffffc0))
	(? (! i) {
		(= o (: sha256))
		(= o$_bf dt)
		(= o$_sz n_sz)
		(@@ o)
	})
	(= new_data (<- (... "sll:hash_sha256")
		sha256$_a
		sha256$_b
		sha256$_c
		sha256$_d
		sha256$_e
		sha256$_f
		sha256$_g
		sha256$_h
		(: dt 0 i)
	))
	(@@ (. sha256_type
		(: dt i len)
		n_sz
		(: new_data 0)
		(: new_data 1)
		(: new_data 2)
		(: new_data 3)
		(: new_data 4)
		(: new_data 5)
		(: new_data 6)
		(: new_data 7)
	))
))

(= sha256_digest (,,, sha256
	(= sha256 (:: sha256 sha256_type))
	(= extra sha256$_bf)
	(= len ($ extra))
	(= padding (- (?: (< len 56) 55 119) len))
	(= n (<< sha256$_sz 3))
	(= extra (+
		extra
		'\x80'
		(* "\x00" padding)
		(:: (>> n 56) char_type)
		(:: (>> n 48) char_type)
		(:: (>> n 40) char_type)
		(:: (>> n 32) char_type)
		(:: (>> n 24) char_type)
		(:: (>> n 16) char_type)
		(:: (>> n 8) char_type)
		(:: n char_type)
	))
	(= sha256_data (<- (... "sll:hash_sha256")
		sha256$_a
		sha256$_b
		sha256$_c
		sha256$_d
		sha256$_e
		sha256$_f
		sha256$_g
		sha256$_h
		extra
	))
	(= o "")
	(-> (= i 0) (< i 8) {
		(= n (: sha256_data i))
		(= o (+ o
			(:: (>> n 24) char_type)
			(:: (>> n 16) char_type)
			(:: (>> n 8) char_type)
			(:: n char_type)
		))
		(++ i)
	})
	(@@ o)
))

(= sha256_process (,,, dt
	(@@ (<- sha256_digest (<- sha256_update (<- sha256_init) dt)))
))



(= sha224_init (,,,
	(@@ (. sha256_type
		""
		0
		0xc1059ed8
		0x367cd507
		0x3070dd17
		0xf70e5939
		0xffc00b31
		0x68581511
		0x64f98fa7
		0xbefa4fa4
	))
))

(= sha224_update sha256_update)

(= sha224_digest (,,, sha224
	(@@ (: (<- sha256_digest sha224) 0 28))
))

(= sha224_process (,,, dt
	(@@ (<- sha224_digest (<- sha224_update (<- sha224_init) dt)))
))



(= sha512_init (,,,
	(@@ (. sha512_type
		""
		0
		0x6a09e667f3bcc908
		0xbb67ae8584caa73b
		0x3c6ef372fe94f82b
		0xa54ff53a5f1d36f1
		0x510e527fade682d1
		0x9b05688c2b3e6c1f
		0x1f83d9abfb41bd6b
		0x5be0cd19137e2179
	))
))

(= sha512_update (,,, sha512 dt
	(= sha512 (:: sha512 sha512_type))
	(? (=== dt nil)
		(@@ (: sha512))
	)
	(= dt (:: dt string_type))
	(= len ($ dt))
	(? (! len)
		(@@ (: sha512))
	)
	(= n_sz (+ sha512$_sz len))
	(= dt (+ sha512$_bf dt))
	(= len (+ len ($ sha512$_bf)))
	(= i (& len 0xffffffffffffff80))
	(? (! i) {
		(= o (: sha512))
		(= o$_bf dt)
		(= o$_sz n_sz)
		(@@ o)
	})
	(= new_data (<- (... "sll:hash_sha512")
		sha512$_a
		sha512$_b
		sha512$_c
		sha512$_d
		sha512$_e
		sha512$_f
		sha512$_g
		sha512$_h
		(: dt 0 i)
	))
	(@@ (. sha512_type
		(: dt i len)
		n_sz
		(: new_data 0)
		(: new_data 1)
		(: new_data 2)
		(: new_data 3)
		(: new_data 4)
		(: new_data 5)
		(: new_data 6)
		(: new_data 7)
	))
))

(= sha512_digest (,,, sha512
	(= sha512 (:: sha512 sha512_type))
	(= extra sha512$_bf)
	(= len ($ extra))
	(= padding (+ (- (?: (< len 112) 111 239) len) 8))
	(= n (<< sha512$_sz 3))
	(= extra (+
		extra
		'\x80'
		(* "\x00" padding)
		(:: (>> n 56) char_type)
		(:: (>> n 48) char_type)
		(:: (>> n 40) char_type)
		(:: (>> n 32) char_type)
		(:: (>> n 24) char_type)
		(:: (>> n 16) char_type)
		(:: (>> n 8) char_type)
		(:: n char_type)
	))
	(= sha512_data (<- (... "sll:hash_sha512")
		sha512$_a
		sha512$_b
		sha512$_c
		sha512$_d
		sha512$_e
		sha512$_f
		sha512$_g
		sha512$_h
		extra
	))
	(= o "")
	(-> (= i 0) (< i 8) {
		(= n (: sha512_data i))
		(= o (+ o
			(:: (>> n 56) char_type)
			(:: (>> n 48) char_type)
			(:: (>> n 40) char_type)
			(:: (>> n 32) char_type)
			(:: (>> n 24) char_type)
			(:: (>> n 16) char_type)
			(:: (>> n 8) char_type)
			(:: n char_type)
		))
		(++ i)
	})
	(@@ o)
))

(= sha512_process (,,, dt
	(@@ (<- sha512_digest (<- sha512_update (<- sha512_init) dt)))
))



(= sha384_init (,,,
	(@@ (. sha512_type
		""
		0
		0xcbbb9d5dc1059ed8
		0x629a292a367cd507
		0x9159015a3070dd17
		0x152fecd8f70e5939
		0x67332667ffc00b31
		0x8eb44a8768581511
		0xdb0c2e0d64f98fa7
		0x47b5481dbefa4fa4
	))
))

(= sha384_update sha512_update)

(= sha384_digest (,,, sha384
	(@@ (: (<- sha512_digest sha384) 0 48))
))

(= sha384_process (,,, dt
	(@@ (<- sha384_digest (<- sha384_update (<- sha384_init) dt)))
))



(= hexdigest (,,, d
	(= l ($ d))
	(= o "")
	(-> (= i 0) (< i l) {
		(= c (: d i))
		(= o (+ o
			(:: (+ (>> c 4) (?: (> (>> c 4) 9) 87 48)) char_type)
			(:: (+ (& c 15) (?: (> (& c 15) 9) 87 48)) char_type)
		))
		(++ i)
	})
	(@@ o)
))



(= hash (. hash_module_type
	[
		MD5_BLOCK_SIZE
		MD5_DIGEST_SIZE
		md5_init
		md5_update
		md5_digest
		md5_process
	]
	[
		SHA1_BLOCK_SIZE
		SHA1_DIGEST_SIZE
		sha1_init
		sha1_update
		sha1_digest
		sha1_process
	]
	[
		SHA224_BLOCK_SIZE
		SHA224_DIGEST_SIZE
		sha224_init
		sha224_update
		sha224_digest
		sha224_process
	]
	[
		SHA256_BLOCK_SIZE
		SHA256_DIGEST_SIZE
		sha256_init
		sha256_update
		sha256_digest
		sha256_process
	]
	[
		SHA384_BLOCK_SIZE
		SHA384_DIGEST_SIZE
		sha384_init
		sha384_update
		sha384_digest
		sha384_process
	]
	[
		SHA512_BLOCK_SIZE
		SHA512_DIGEST_SIZE
		sha512_init
		sha512_update
		sha512_digest
		sha512_process
	]
	hexdigest
))
(## hash)
