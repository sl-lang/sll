(---
	"@build-script/config.sll"
	"@build-script/hashlist.sll"
	"@build-script/util.sll"
	"array.sll"
	"file.sll"
	"log.sll"
	"path.sll"
	"process.sll"
	"string.sll"
	"sys.sll"
	"types.sll"
)



(= build_module_type (&:
	(~ func_type) build_modules
	(~ func_type) build_sll
	(~ func_type) build_sll_cli
))



(= _replace_marker (,,, args marker value
	(-> nil true
		(= idx (<- array$index args marker))
		(? (=== idx -1) (@@ args))
		(= args (+
			(: args 0 idx)
			(?: (=== (:? value) string_type)
				[value]
				value
			)
			(>> args (+ idx 1))
		))
	)
))

(= _execute_command (,,, args input output options
	(? (! args) (@@))
	(= args (+ (<- _replace_marker
		(<- _replace_marker
			args
			config$COMMAND_MARKER_INPUT
			input
		)
		config$COMMAND_MARKER_OUTPUT
		output
	) (|| options [])))
	(? (<- util$execute args) (<- util$fail_and_exit))
))

(= build_modules (,,,
	(<- log$log "Compiling modules...")
	(<- log$log "  Listing files...")
	(= module_files (<- path$list_dir config$BUILTIN_LIBRARY_ROOT_FILE_PATH false))
	(<- log$log "    Found " ($ module_files) " files")
	(<- log$log "  Compiling files...")
	(<- _execute_command config$MODULE_COMPILATION_COMMAND module_files nil)
	(= output_files ([> (= i 0) (< i ($ module_files))
		(= name (: module_files i))
		(++ i)
		(? (=== (: name 0) '_') (<<<))
		(+ "build/lib/" name ".slc")
	))
	(<- log$log "  Generating bundle...")
	(<- _execute_command config$MODULE_BUNDLE_GENERATION_COMMAND output_files nil)
	(<- log$log "  Generating debug bundle...")
	(<- _execute_command config$MODULE_DEBUG_BUNDLE_GENERATION_COMMAND output_files nil)
	(<- log$log "  Removing compilation files...")
	(-> (= i 0) (< i ($ module_files))
		(= file_path (+ "build/lib/" (: module_files i) ".slc"))
		(<- log$log "    Removing '" file_path "'...")
		(<- file$delete file_path)
		(++ i)
	)
))

(= build_sll (,,,
	(<- log$log "Compiling...")
	(<- log$log "  Listing files...")
	(= files (<- path$list_dir config$ROOT_SOURCE_FILE_PATH true))
	(= files (+
		([> (= i 0) (< i ($ files))
			(= fp (: files i))
			(++ i)
			(? (|: fp "platform") (<<<))
			fp
		)
		(<- path$list_dir (: config$ROOT_PLATFORM_SOURCE_FILE_PATH sys$PLATFORM) true)
	))
	(= files ([> (= i 0) (< i ($ files))
		(= fp (: files i))
		(++ i)
		(? (! (|: config$SOURCE_FILE_EXTENSIONS (>> fp (<- string$index_reverse fp '.')))) (<<<))
		fp
	))
	(<- log$log "    Found " ($ files) " files")
	(<- log$log "  Aggregating preprocessor definitions...")
	(= options [])
	(-> (= i 0) (< i ($ config$PREPROCESSOR_DEFINITIONS))
		(<- array$extend options [
			(: config$PREPROCESSOR_DEFINITION_PREFIX sys$PLATFORM)
			(: config$PREPROCESSOR_DEFINITIONS i)
		])
		(++ i)
	)
	(= command_list (: config$COMMANDS (+
		sys$PLATFORM
		(* "_release" config$OPTIONS$RELEASE)
	)))
	(= output_files [])
	(<- log$log "  Compiling files...")
	(= err false)
	(-> (= i 0) (< i ($ files))
		(= file_path (: files i))
		(++ i)
		(= output_file_path (<- util$get_output_file_path file_path))
		(<- array$push output_files output_file_path)
		(? (! (<- hashlist$check output_file_path)) (<<<))
		(<- log$log "    " file_path)
		(? (?: (=== (>> file_path (<- string$index_reverse file_path '.')) ".asm")
			(||
				(<- _execute_command command_list$NASM_COMPILATION_COMMAND file_path output_file_path)
				(<- _execute_command command_list$NASM_POST_COMPILATION_COMMAND file_path output_file_path)
			)
			(<- _execute_command command_list$COMPILATION_COMMAND file_path (+ command_list$OUTPUT_PREFIX output_file_path) options)
		) {
			(<- hashlist$fail output_file_path)
			(= err true)
		})
	)
	(? err (<- util$fail_and_exit))
	(<- log$log "  Linking files...")
	(<- _execute_command command_list$LINK_COMMAND output_files nil)
	(<- log$log "  Executing post-link command...")
	(<- _execute_command command_list$POST_LINK_COMMAND nil nil)
))

(= build_sll_cli (,,,
	(<- log$log "Compiling CLI...")
	(= command_list (: config$CLI_COMMANDS sys$PLATFORM))
	(<- log$log "  Executing pre-compilation command...")
	(<- _execute_command command_list$PRE_COMPILATION_COMMAND nil nil)
	(<- log$log "  Compiling files...")
	(<- _execute_command command_list$COMPILATION_COMMAND nil nil)
	(<- log$log "  Linking files...")
	(<- _execute_command command_list$LINK_COMMAND nil nil)
	(<- _execute_command command_list$LINK_HEADLESS_COMMAND nil nil)
	(<- log$log "  Executing post-link command...")
	(<- _execute_command command_list$POST_LINK_COMMAND nil nil)
))



(= build (. build_module_type
	build_modules
	build_sll
	build_sll_cli
))
(## build)
