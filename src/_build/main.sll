(---
	"@build-script/api.sll"
	"@build-script/assembly.sll"
	"@build-script/build.sll"
	"@build-script/config.sll"
	"@build-script/documentation.sll"
	"@build-script/hashlist.sll"
	"@build-script/header.sll"
	"@build-script/operator_parser.sll"
	"@build-script/optimizer.sll"
	"@build-script/util.sll"
	"@build-script/website.sll"
	"array.sll"
	"base64.sll"
	"file.sll"
	"path.sll"
	"string.sll"
	"sys.sll"
)



(? config$OPTIONS$FUZZER_DEBUG {
	(= file_path nil)
	(-> (= i 1) (< i ($ sys$ARGV))
		(= arg (: sys$ARGV i))
		(? (!== (: arg 0 2) "--") {
			(= file_path arg)
			(@)
		})
		(++ i)
	)
	(? file_path {
		(= data (<- string$split
			(<- file$read
				(<- file$open file_path "r")
			)
			'\n'
		))
		(= args ["build/sll_debug_fuzzer"])
		(-> (= i 0) (< i ($ data))
			(= line (: data i))
			(++ i)
			(? (!== (: line 0 5) "$$$$$") (<<<))
			(<- array$push args (<- base64$decode
				(<- string$trim
					(:
						(<- string$split
							line
							':'
						)
						1
					)
				)
			))
		)
		(<- util$execute (+ (: config$DEBUGGER_COMMAND sys$PLATFORM) args))
	})
	(@@)
})
(<- file$write
	(<- file$open "build/version" "w")
	config$VERSION
)
(<- util$create_output_dir)
(<- assembly$generate_assembly_optimizer)
(<- optimizer$generate_optimizer)
(<- operator_parser$generate_operator_parser)
(<- header$generate_error_headers)
(= docs (<- documentation$parse_documentation_files))
(<- api$generate_function_table docs)
(? config$OPTIONS$WEBSITE {
	(<- website$generate_website docs)
	(@@)
})
(<- header$generate_library_header docs)
(<- hashlist$init)
(<- build$build_sll)
(<- build$build_sll_cli)
(<- build$build_modules)
(<- build$build_extensions)
(? config$OPTIONS$FUZZER {
	(<- path$mkdir "build/fuzzer_data")
	(<- path$mkdir "build/fuzzer_output")
	(? (||
		(<- util$execute config$FUZZER_COMPILATION_COMMAND)
		(<- util$execute config$FUZZER_DEBUGGER_COMPILATION_COMMAND)
		(<- util$execute config$FUZZER_EXECUTION_COMMAND)
	)(<- util$fail_and_exit))
	(@@)
})
(= output_executable (+ "build/sll" sys$EXECUTABLE_EXTENSION))
(? config$OPTIONS$TEST
	(<- util$execute [output_executable "tests/_runner.sll"])
)
(? config$OPTIONS$RUN {
	(? (||
		(<- util$execute [output_executable "-h"])
		(<- util$execute (+ [output_executable "-v" "-O" "-c" "-o" "build/test" "-e" "-R" "examples/_internal_test/test.sll" "-I" "@internal|examples/_internal_test"] (* ["-r"] config$OPTIONS$RELEASE)))
		(<- util$execute [output_executable "build/test.slc" "-v" "-O" "-x" "5" "-e" "-a" "-c" "-o" "build/test2" "-R"])
		(<- util$execute (+
			(?: (: sys$ENVIRONMENT "SLL_DEBUGGER") (: config$DEBUGGER_COMMAND sys$PLATFORM) [])
			[output_executable "build/test2.sla" "-v" "-P"]
		))
	) (<- util$fail_and_exit))
})
