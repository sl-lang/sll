(---
	"string.sll"
	"types.sll"
)



(= window_geometry_type (&:
	(,,, dt
		(? (< dt$width 0) {
			(= dt$width (- 0 dt$width))
			(= dt$x (- dt$x dt$width))
		})
		(? (< dt$height 0) {
			(= dt$height (- 0 dt$height))
			(= dt$y (- dt$y dt$height))
		})
	) @@init@@
	(,,, dt
		(@@ (<- string$format "%llux%llu%+lld%+lld"
			dt$width
			dt$height
			dt$x
			dt$y
		))
	) @@string@@
	int_type x
	int_type y
	int_type width
	int_type height
))

(= window_geometry_module_type (&:
	(~ type_type) window_geometry_type
	(~ func_type) from_corners
))



(= from_corners (,,, x1 y1 x2 y2
	(? (< x2 x1) {
		(= tmp x1)
		(= x1 x2)
		(= x2 tmp)
	})
	(? (< y2 y1) {
		(= tmp y1)
		(= y1 y2)
		(= y2 tmp)
	})
	(@@ (. window_geometry_type
		x1
		y1
		(- x2 x1)
		(- y2 y1)
	))
))



(= window_geometry (. window_geometry_module_type
	window_geometry_type
	from_corners
))
(## window_geometry)
