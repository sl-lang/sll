(---
	"string.sll"
	"types.sll"
	"weakref.sll"
	"window/_load_library.sll"
	"window/geometry.sll"
)



(# set_title show)



(= window_window_type (&:
	(,,, dt
		(@@ (+
			'<'
			(:: dt$geometry string_type)
			'>'
		))
	) @@string@@
	(,,, dt
		(<- (... "window:window_destroy") dt$id)
	) @@delete@@
	(~ int_type) id
	(~ window_geometry$window_geometry_type) geometry
))

(= window_window_module_type (&:
	(~ type_type) window_window_type
	(~ func_type) create
	(~ func_type) destroy
	(~ func_type) hide
	(~ func_type) poll
	(~ func_type) set_title
	(~ func_type) show
))



(= _window_map <>)



(= create (,,, geometry parent title visible?
	(= geometry (:: geometry window_geometry$window_geometry_type))
	(= parent_id (?: parent
		(: (:: parent window_window_type) "id")
		0xffffffffffffffff
	))
	(= id (<- (... "window:window_create")
		geometry$x
		geometry$y
		geometry$width
		geometry$height
		parent_id
	))
	(= out (. window_window_type
		id
		geometry
	))
	(= (: _window_map id) (<- weakref$ref out))
	(? title (<- set_title out title))
	(? visible? (<- show out))
	(@@ out)
))

(= destroy (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- (... "window:window_destroy") window$id)
))

(= hide (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- (... "window:window_set_visibility") window$id false)
))

(= poll (,,,
	(= data (<- (... "window:window_poll_events")))
	(-> (= i 0) (< i ($ data))
		(= evt (: data i))
		(++ i)
	)
))

(= set_title (,,, window title
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= title (?: title (:: title string_type) ""))
	(= title (: (<- string$split title '\x00') 0))
	(<- (... "window:window_set_title")
		window$id
		title
	)
))

(= show (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- (... "window:window_set_visibility") window$id true)
))



(= window_window (. window_window_module_type
	window_window_type
	create
	destroy
	hide
	poll
	set_title
	show
))
(## window_window)
