(---
	"array.sll"
	"audit.sll"
	"map.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
	"window/_load_library.sll"
	"window/geometry.sll"
)



(# destroy set_title set_visibility)



(= window_callbacks_type (&:
	func_type button
	func_type button_down
	func_type button_up
	func_type focus
	func_type focus_enter
	func_type focus_leave
	func_type geometry
	func_type key
	func_type key_down
	func_type key_up
	func_type mouse
	func_type mouse_enter
	func_type mouse_leave
	func_type mouse_state
	func_type position
	func_type redraw
	func_type scroll
	func_type size
))

(= window_window_type (&:
	(,,, dt
		(@@ (+ '<' (:: dt$geometry string_type) '>'))
	) @@string@@
	(,,, dt
		(<- destroy dt)
	) @@delete@@
	(~ int_type) id
	window_geometry$window_geometry_type geometry
	int_type flags
	string_type title
	int_type mouse_x
	int_type mouse_y
	window_callbacks_type callbacks
	array_type _keys
))

(= window_window_module_type (&:
	(~ type_type) window_window_type
	(~ int_type) FLAG_IN_FOCUS
	(~ int_type) FLAG_HAS_MOUSE
	(~ int_type) FLAG_BUTTON_1
	(~ int_type) FLAG_BUTTON_2
	(~ int_type) FLAG_BUTTON_3
	(~ int_type) FLAG_BUTTON_LEFT
	(~ int_type) FLAG_BUTTON_MIDDLE
	(~ int_type) FLAG_BUTTON_RIGHT
	(~ int_type) FLAG_BUTTON_4
	(~ int_type) FLAG_BUTTON_5
	(~ int_type) FLAG_BUTTON_6
	(~ int_type) SCROLL_DOWN
	(~ int_type) SCROLL_LEFT
	(~ int_type) SCROLL_RIGHT
	(~ int_type) SCROLL_UP
	(~ func_type) create
	(~ func_type) destroy
	(~ func_type) geometry
	(~ func_type) get
	(~ func_type) hide
	(~ func_type) poll
	(~ func_type) set_title
	(~ func_type) set_visibility
	(~ func_type) show
))



(= KEY_COUNT 256)

(= FLAG_IN_FOCUS 1)
(= FLAG_HAS_MOUSE 2)
(= FLAG_BUTTON_1 4)
(= FLAG_BUTTON_2 8)
(= FLAG_BUTTON_3 16)
(= FLAG_BUTTON_LEFT FLAG_BUTTON_1)
(= FLAG_BUTTON_MIDDLE FLAG_BUTTON_2)
(= FLAG_BUTTON_RIGHT FLAG_BUTTON_3)
(= FLAG_BUTTON_4 32)
(= FLAG_BUTTON_5 64)
(= FLAG_BUTTON_6 128)

(= SCROLL_DOWN 0)
(= SCROLL_UP 1)
(= SCROLL_RIGHT 2)
(= SCROLL_LEFT 3)



(= _window_map <>)



(= create (,,, geometry parent title visible?
	(= geometry (:: geometry window_geometry$window_geometry_type))
	(= parent_id (?: parent
		(: (:: parent window_window_type) "id")
		0xffffffffffffffff
	))
	(= id (<- (... "window:window_create")
		geometry$x
		geometry$y
		geometry$width
		geometry$height
		parent_id
	))
	(<- audit$audit "window.create" "i" id)
	(= out (. window_window_type
		id
		geometry
		0
		""
		0
		0
		[]
		(<- array$create (>> (+ KEY_COUNT 63) 6))
	))
	(= (: _window_map id) (<- weakref$ref out))
	(? title (<- set_title out title))
	(? visible? (<- set_visibility out true))
	(@@ out)
))

(= destroy (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- audit$audit "window.destroy" "i" window$id)
	(<- (... "window:window_destroy") window$id)
	(<- map$remove _window_map window$id)
))

(= geometry (,,, window geometry_
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= geometry_ (:: geometry_ window_geometry$window_geometry_type))
	(= window$geometry geometry_)
	(<- (... "window:window_set_geometry")
		window$id
		geometry_$x
		geometry_$y
		geometry_$width
		geometry_$height
	)
))

(= get (,,, id
	(= window (: _window_map id))
	(@@ (?: window
		(<- weakref$get window)
		nil
	))
))

(= hide (,,, window
	(<- set_visibility window false)
))

(= poll (,,, blocking?
	(= data (<- (... "window:window_poll_events")
		(!! blocking?)
	))
	(-> (= i 0) (< i ($ data))
		(= event (: data i))
		(++ i)
		(= window (<- weakref$get (: _window_map (: event 1))))
		(? (=== window weakref$WEAKREF_NO_OBJECT) (<<<))
		(= type (: event 0))
		(= event (>> event 2))
		(?? type
			1 {
				(= key (: event 0))
				(= state (: event 1))
				(= idx (>> key 6))
				(= mask (& key 63))
				(= value (: window$_keys idx))
				(= (: window$_keys idx) (?: state
					(| value mask)
					(& value (~ mask))
				))
				(<- window$callbacks$key window key state)
				(<- (?: state
					window$callbacks$key_down
					window$callbacks$key_up
				) window key)
			}
			2 {
				(= button (: event 0))
				(= state (: event 1))
				(= mask (<< FLAG_BUTTON_1 button))
				(= value (: window$_buttons idx))
				(= (: window$_buttons idx) (?: state
					(| value mask)
					(& value (~ mask))
				))
				(<- window$callbacks$button window button state)
				(<- (?: state
					window$callbacks$button_down
					window$callbacks$button_up
				) window button)
			}
			3 {
				(= window$mouse_x (: event 0))
				(= window$mouse_y (: event 1))
				(<- window$callbacks$mouse window (: event 0) (: event 1))
			}
			4 {
				(= state (: event 0))
				(= window$flags (?: state
					(| window$flags FLAG_HAS_MOUSE)
					(& window$flags (~ FLAG_HAS_MOUSE))
				))
				(<- window$callbacks$mouse_state window state)
				(<- (?: state
					window$callbacks$mouse_enter
					window$callbacks$mouse_leave
				) window)
			}
			5 {
				(= state (: event 0))
				(= window$flags (?: state
					(| window$flags FLAG_IN_FOCUS)
					(& window$flags (~ FLAG_IN_FOCUS))
				))
				(<- window$callbacks$focus window state)
				(<- (?: state
					window$callbacks$focus_enter
					window$callbacks$focus_leave
				) window)
			}
			6 {
				(= geometry (:: event window_geometry$window_geometry_type))
				(= window$geometry geometry)
				(<- window$callbacks$geometry window geometry)
				(<- window$callbacks$position window (: event 0) (: event 1))
				(<- window$callbacks$size window (: event 2) (: event 3))
			}
			7 {
				(<- window$callbacks$scroll window (: event 0))
			}
			8 {
				(<- window$callbacks$redraw window (: event 0) (: event 1) (: event 2) (: event 3))
			}
		)
	)
))

(= set_title (,,, window title
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= title (?: title (:: title string_type) ""))
	(= title (: (<- string$split title '\x00') 0))
	(= window$title title)
	(<- (... "window:window_set_title")
		window$id
		title
	)
))

(= set_visibility (,,, window show?
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- (... "window:window_set_visibility") window$id show?)
))

(= show (,,, window
	(<- set_visibility window true)
))



(= window_window (. window_window_module_type
	window_window_type
	FLAG_IN_FOCUS
	FLAG_HAS_MOUSE
	FLAG_BUTTON_1
	FLAG_BUTTON_2
	FLAG_BUTTON_3
	FLAG_BUTTON_LEFT
	FLAG_BUTTON_MIDDLE
	FLAG_BUTTON_RIGHT
	FLAG_BUTTON_4
	FLAG_BUTTON_5
	FLAG_BUTTON_6
	SCROLL_DOWN
	SCROLL_LEFT
	SCROLL_RIGHT
	SCROLL_UP
	create
	destroy
	geometry
	get
	hide
	poll
	set_title
	set_visibility
	show
))
(## window_window)
