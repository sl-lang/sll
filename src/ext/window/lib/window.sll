(---
	"audit.sll"
	"map.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
	"window/_load_library.sll"
	"window/geometry.sll"
)



(# destroy set_title show)



(= window_window_type (&:
	(,,, dt
		(@@ (+ '<' (:: dt$geometry string_type) '>'))
	) @@string@@
	(,,, dt
		(<- destroy dt)
	) @@delete@@
	(~ int_type) id
	window_geometry$window_geometry_type geometry
	string_type title
))

(= window_window_module_type (&:
	(~ type_type) window_window_type
	(~ func_type) create
	(~ func_type) destroy
	(~ func_type) geometry
	(~ func_type) hide
	(~ func_type) poll
	(~ func_type) set_title
	(~ func_type) show
))



(= _window_map <>)



(= create (,,, geometry parent title visible?
	(= geometry (:: geometry window_geometry$window_geometry_type))
	(= parent_id (?: parent
		(: (:: parent window_window_type) "id")
		0xffffffffffffffff
	))
	(= id (<- (... "window:window_create")
		geometry$x
		geometry$y
		geometry$width
		geometry$height
		parent_id
	))
	(<- audit$audit "window.create" "i" id)
	(= out (. window_window_type
		id
		geometry
		""
	))
	(= (: _window_map id) (<- weakref$ref out))
	(? title (<- set_title out title))
	(? visible? (<- show out))
	(@@ out)
))

(= destroy (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- audit$audit "window.destroy" "i" window$id)
	(<- (... "window:window_destroy") window$id)
	(<- map$remove _window_map window$id)
))

(= geometry_ (,,, window geometry
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= geometry (:: geometry window_geometry$window_geometry_type))
	(= window$geometry geometry)
	(<- (... "window:window_set_geometry")
		window$id
		geometry$x
		geometry$y
		geometry$width
		geometry$height
	)
))

(= hide (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- (... "window:window_set_visibility") window$id false)
))

(= poll (,,, blocking?
	(= data (<- (... "window:window_poll_events")
		(!! blocking?)
	))
	(-> (= i 0) (< i ($ data))
		(= evt (: data i))
		(++ i)
	)
))

(= set_title (,,, window title
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= title (?: title (:: title string_type) ""))
	(= title (: (<- string$split title '\x00') 0))
	(= window$title title)
	(<- (... "window:window_set_title")
		window$id
		title
	)
))

(= show (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(<- (... "window:window_set_visibility") window$id true)
))



(= window_window (. window_window_module_type
	window_window_type
	create
	destroy
	geometry_
	hide
	poll
	set_title
	show
))
(## window_window)
