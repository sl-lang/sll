(---
	"array.sll"
	"audit.sll"
	"map.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
	"window/_load_library.sll"
	"window/geometry.sll"
	"window/range.sll"
)



(# destroy set_title set_visibility)



(= window_callbacks_type (&:
	func_type button
	func_type button_down
	func_type button_up
	func_type ignore_close_request?
	func_type destroy
	func_type focus
	func_type focus_enter
	func_type focus_leave
	func_type geometry
	func_type key
	func_type key_down
	func_type key_up
	func_type mouse
	func_type mouse_x
	func_type mouse_y
	func_type mouse_enter
	func_type mouse_leave
	func_type mouse_state
	func_type position
	func_type redraw
	func_type scroll
	func_type scroll_x
	func_type scroll_y
	func_type size
	func_type state
))

(= window_inputs_type (&:
	int_type mouse_x
	int_type mouse_y
	array_type _keys
))

(= window_layout_type (&:
	window_geometry$window_geometry_type geometry
	window_range$window_range_type width_constraints
	window_range$window_range_type height_constraints
	string_type title
	int_type state
))

(= window_window_type (&:
	(,,, data
		(@@ (+ '<' (:: data$layout$geometry string_type) '>'))
	) @@string@@
	(,,, data
		(<- destroy data)
	) @@delete@@
	(~ int_type) id
	int_type flags
	window_layout_type layout
	window_inputs_type inputs
	window_callbacks_type callbacks
))

(= window_window_module_type (&:
	(~ type_type) window_window_type
	(~ int_type) FLAG_BUTTON_1
	(~ int_type) FLAG_BUTTON_2
	(~ int_type) FLAG_BUTTON_3
	(~ int_type) FLAG_BUTTON_4
	(~ int_type) FLAG_BUTTON_5
	(~ int_type) FLAG_BUTTON_6
	(~ int_type) FLAG_BUTTON_LEFT
	(~ int_type) FLAG_BUTTON_MIDDLE
	(~ int_type) FLAG_BUTTON_RIGHT
	(~ int_type) FLAG_CLOSED
	(~ int_type) FLAG_HAS_MOUSE
	(~ int_type) FLAG_IN_FOCUS
	(~ int_type) FLAG_SHOWN
	(~ int_type) SCROLL_DOWN
	(~ int_type) SCROLL_LEFT
	(~ int_type) SCROLL_RIGHT
	(~ int_type) SCROLL_UP
	(~ int_type) STATE_FULLSCREEN
	(~ int_type) STATE_MAXIMIZED
	(~ int_type) STATE_MINIMIZED
	(~ int_type) STATE_NORMAL
	(~ func_type) button_pressed
	(~ func_type) create
	(~ func_type) destroy
	(~ func_type) geometry
	(~ func_type) get
	(~ func_type) hide
	(~ func_type) key_pressed
	(~ func_type) poll
	(~ func_type) set_size_constraints
	(~ func_type) set_state
	(~ func_type) set_title
	(~ func_type) set_visibility
	(~ func_type) show
))



(= KEY_COUNT 256)

(= FLAG_IN_FOCUS 1)
(= FLAG_HAS_MOUSE 2)
(= FLAG_CLOSED 4)
(= FLAG_SHOWN 8)
(= FLAG_BUTTON_1 16)
(= FLAG_BUTTON_2 (<< FLAG_BUTTON_1 1))
(= FLAG_BUTTON_3 (<< FLAG_BUTTON_1 2))
(= FLAG_BUTTON_4 (<< FLAG_BUTTON_1 3))
(= FLAG_BUTTON_5 (<< FLAG_BUTTON_1 4))
(= FLAG_BUTTON_6 (<< FLAG_BUTTON_1 5))
(= FLAG_BUTTON_LEFT FLAG_BUTTON_1)
(= FLAG_BUTTON_MIDDLE FLAG_BUTTON_2)
(= FLAG_BUTTON_RIGHT FLAG_BUTTON_3)

(= SCROLL_DOWN 0)
(= SCROLL_UP 1)
(= SCROLL_RIGHT 2)
(= SCROLL_LEFT 3)

(= STATE_FULLSCREEN 3)
(= STATE_MAXIMIZED 2)
(= STATE_MINIMIZED 1)
(= STATE_NORMAL 0)



(= _window_map <>)



(= button_pressed (,,, window button
	(? (! window) (@@ false))
	(= window (:: window window_window_type))
	(= button (:: button int_type))
	(? (< button 1) (@@ false))
	(@@ (!! (&
		window$flags
		(<< FLAG_BUTTON_1 (- button 1))
	)))
))

(= create (,,, geometry parent title visible?
	(= geometry (:: geometry window_geometry$window_geometry_type))
	(= parent_id (?: parent
		(: (:: parent window_window_type) "id")
		0xffffffffffffffff
	))
	(= id (<- (... "window:window_create")
		geometry$x
		geometry$y
		geometry$width
		geometry$height
		parent_id
	))
	(<- audit$audit "window.create" "i" id)
	(= out (. window_window_type
		id
	))
	(= out$layout$geometry geometry)
	(= out$layout$state STATE_NORMAL)
	(= out$inputs$_keys (<- array$create (>> (+ KEY_COUNT 63) 6)))
	(= (: _window_map id) (<- weakref$ref out))
	(? title (<- set_title out title))
	(? visible? (<- set_visibility out true))
	(@@ out)
))

(= destroy (,,, window
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(? (& window$flags FLAG_CLOSED) (@@))
	(= window$flags (| window$flags FLAG_CLOSED))
	(<- audit$audit "window.destroy" "i" window$id)
	(<- (... "window:window_destroy") window$id)
	(<- window$callbacks$destroy window)
))

(= geometry (,,, window geometry_
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= geometry_ (:: geometry_ window_geometry$window_geometry_type))
	(= window$layout$geometry geometry_)
	(<- (... "window:window_set_geometry")
		window$id
		geometry_$x
		geometry_$y
		geometry_$width
		geometry_$height
	)
))

(= get (,,, id
	(= window (: _window_map id))
	(@@ (?: window
		(<- weakref$get window)
		nil
	))
))

(= hide (,,, window
	(<- set_visibility window false)
))

(= key_pressed (,,, window key
	(? (! window) (@@ false))
	(= window (:: window window_window_type))
	(= key (:: key int_type))
	(@@ (!! (&
		(: window$inputs$_keys (>> key 6))
		(<< 1 (& key 63))
	)))
))

(= poll (,,, blocking?
	(= data (<- (... "window:window_poll_events")
		(!! blocking?)
	))
	(-> (= i 0) (< i ($ data))
		(= event (: data i))
		(++ i)
		(= window (<- weakref$get (: _window_map (: event 1))))
		(? (=== window weakref$WEAKREF_NO_OBJECT) (<<<))
		(= type (: event 0))
		(= event (>> event 2))
		(?? type
			1 {
				(= key (: event 0))
				(= state (: event 1))
				(= idx (>> key 6))
				(= mask (& key 63))
				(= value (: window$inputs$_keys idx))
				(= (: window$inputs$_keys idx) (?: state
					(| value mask)
					(& value (~ mask))
				))
				(<- window$callbacks$key window key state)
				(<- (?: state
					window$callbacks$key_down
					window$callbacks$key_up
				) window key)
			}
			2 {
				(= button (: event 0))
				(= state (: event 1))
				(= mask (<< FLAG_BUTTON_1 button))
				(= value (: window$inputs$_buttons idx))
				(= (: window$inputs$_buttons idx) (?: state
					(| value mask)
					(& value (~ mask))
				))
				(<- window$callbacks$button window button state)
				(<- (?: state
					window$callbacks$button_down
					window$callbacks$button_up
				) window button)
			}
			3 {
				(= window$inputs$mouse_x (: event 0))
				(= window$inputs$mouse_y (: event 1))
				(<- window$callbacks$mouse window (: event 0) (: event 1))
				(? (: event 0) (< window$callbacks$mouse_x (: event 0)))
				(? (: event 1) (< window$callbacks$mouse_y (: event 1)))
			}
			4 {
				(= state (: event 0))
				(= window$flags (?: state
					(| window$flags FLAG_HAS_MOUSE)
					(& window$flags (~ FLAG_HAS_MOUSE))
				))
				(<- window$callbacks$mouse_state window state)
				(<- (?: state
					window$callbacks$mouse_enter
					window$callbacks$mouse_leave
				) window)
			}
			5 {
				(= state (: event 0))
				(= window$flags (?: state
					(| window$flags FLAG_IN_FOCUS)
					(& window$flags (~ FLAG_IN_FOCUS))
				))
				(<- window$callbacks$focus window state)
				(<- (?: state
					window$callbacks$focus_enter
					window$callbacks$focus_leave
				) window)
			}
			6 {
				(= geometry (:: event window_geometry$window_geometry_type))
				(= window$layout$geometry geometry)
				(<- window$callbacks$geometry window geometry)
				(<- window$callbacks$position window (: event 0) (: event 1))
				(<- window$callbacks$size window (: event 2) (: event 3))
			}
			7 {
				(<- window$callbacks$scroll window (: event 0))
				(?? (: event 0)
					SCROLL_DOWN (<- window$callbacks$scroll_y -1)
					SCROLL_UP (<- window$callbacks$scroll_y 1)
					SCROLL_RIGHT (<- window$callbacks$scroll_x 1)
					SCROLL_LEFT (<- window$callbacks$scroll_x -1)
				)
			}
			8 {
				(<- window$callbacks$redraw window (: event 0) (: event 1) (: event 2) (: event 3))
			}
			9 {
				(? (! (<- window$callbacks$ignore_close_request? window)) (<- destroy window))
			}
			10 {
				(= window$layout$state (: event 0))
				(<- window$callbacks$state window (: event 0))
			}
		)
	)
))

(= set_size_constraints (,,, window width height
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= width (:: width window_range$window_range_type))
	(= height (:: height window_range$window_range_type))
	(? (&&
		(=== window$layout$width_constraints width)
		(=== window$layout$height_constraints height)
	) (@@))
	(= window$layout$width_constraints width)
	(= window$layout$height_constraints height)
	(= new_size [window$layout$geometry$width window$layout$geometry$height])
	(? (< (: new_size 0) width$min) (= (: new_size 0) width$min))
	(? (> (: new_size 0) width$max) (= (: new_size 0) width$max))
	(? (< (: new_size 1) height$min) (= (: new_size 1) height$min))
	(? (> (: new_size 1) height$max) (= (: new_size 1) height$max))
	(<- (... "window:window_set_geometry")
		window$id
		window$layout$geometry$x
		window$layout$geometry$y
		(: new_size 0)
		(: new_size 1)
	)
	(<- (... "window:window_set_size_constraints")
		window$id
		width$min
		height$min
		width$max
		height$max
	)
))

(= set_state (,,, window state
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= state (:: state int_type))
	(? (&&
		(!== state STATE_FULLSCREEN)
		(!== state STATE_MAXIMIZED)
		(!== state STATE_MINIMIZED)
	) (= state STATE_NORMAL))
	(? (=== state window$layout$state) (@@))
	(<- (... "window:window_set_state")
		window$id
		state
	)
))

(= set_title (,,, window title
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= title (?: title (:: title string_type) ""))
	(= title (: (<- string$split title '\x00') 0))
	(= window$layout$title title)
	(<- (... "window:window_set_title")
		window$id
		title
	)
))

(= set_visibility (,,, window show?
	(? (! window) (@@))
	(= window (:: window window_window_type))
	(= window$flags (?: show?
		(| window$flags FLAG_SHOWN)
		(& window$flags (~ FLAG_SHOWN))
	)
	(<- (... "window:window_set_visibility") window$id show?)
))

(= show (,,, window
	(<- set_visibility window true)
))



(= window_window (. window_window_module_type
	window_window_type
	FLAG_BUTTON_1
	FLAG_BUTTON_2
	FLAG_BUTTON_3
	FLAG_BUTTON_4
	FLAG_BUTTON_5
	FLAG_BUTTON_6
	FLAG_BUTTON_LEFT
	FLAG_BUTTON_MIDDLE
	FLAG_BUTTON_RIGHT
	FLAG_CLOSED
	FLAG_HAS_MOUSE
	FLAG_IN_FOCUS
	FLAG_SHOWN
	SCROLL_DOWN
	SCROLL_LEFT
	SCROLL_RIGHT
	SCROLL_UP
	STATE_FULLSCREEN
	STATE_MAXIMIZED
	STATE_MINIMIZED
	STATE_NORMAL
	button_pressed
	create
	destroy
	geometry
	get
	hide
	key_pressed
	poll
	set_size_constraints
	set_state
	set_title
	set_visibility
	show
))
(## window_window)
