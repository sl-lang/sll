(--- "sys.sll" "types.sll" "string.sll" "__generated_sll_ext_debug.sll")


(# get_call_stack)



(= location_type (&:
	(~ string_type) file
	(~ string_type) function
	(~ int_type) line
))

(= call_stack_raw_type (&:
	(~ int_type) return_address
	(~ int_type) stack_offset
))

(= call_stack_type (&:
	(~ string_type) name
	(~ location_type) return
	(~ call_stack_raw_type) raw
))

(= vm_config_type (&:
	(~ int_type) stack_size
	(~ int_type) call_stack_size
	(~ int_type) in
	(~ int_type) out
	(~ int_type) err
))

(= debug_module_type (&:
	(~ type_type) call_stack_type
	(~ type_type) location_type
	(~ type_type) vm_config_type
	(~ func_type) backtrace
	(~ func_type) get_call_stack
	(~ func_type) get_instruction_count
	(~ func_type) get_instruction_index
	(~ func_type) get_location
	(~ func_type) get_name
	(~ func_type) get_ref_count
	(~ func_type) get_vm_config
))



(= ext_fp (+ "sll-ext-debug-" sys$VERSION$major "." sys$VERSION$minor "." sys$VERSION$patch))
(? (! (<- sys$load_library ext_fp __SLL_EXT_DEBUG_LIBRARY)) {
	(:> "Failed to load extension library: " ext_fp "\n")
	(@@ 1)
})



(= backtrace (,,,
	(= o "")
	(= c_st (<- get_call_stack 1))
	(-> (= i ($ c_st)) i {
		(-- i)
		(= k (: c_st i))
		(= o (+ o (<- string$format "at %s:%u (%s)" k$return$file k$return$line k$return$function)))
		(? i (= o (+ o '\n')))
	})
	(@@ o)
))

(= get_call_stack (,,, pop
	(= pop (:: pop int_type))
	(? (< pop 0) (= pop 0))
	(= call_stack (<- (... "sll_ext_debug:get_call_stack") pop))
	(@@ ([> (= i 0) (< i ($ call_stack))
		(= k (: call_stack i))
		(++ i)
		(:: k call_stack_type)
	))
))

(= get_instruction_count (... "sll_ext_debug:get_instruction_count"))

(= get_instruction_index (... "sll_ext_debug:get_instruction_index"))

(= get_location (,,,
	(@@ (:: (<- (... "sll_ext_debug:get_location")) location_type))
))

(= get_name (... "sll_ext_debug:get_name"))

(= get_ref_count (... "sll_ext_debug:get_ref_count"))

(= get_vm_config  (,,,
	(@@ (:: (<- (... "sll_ext_debug:get_vm_config")) vm_config_type))
))



(= debug (. debug_module_type
	call_stack_type
	location_type
	vm_config_type
	backtrace
	get_call_stack
	get_instruction_count
	get_instruction_index
	get_location
	get_name
	get_ref_count
	get_vm_config
))
(## debug)
