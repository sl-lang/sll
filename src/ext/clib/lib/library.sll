(---
	"clib/_load_library.sll"
	"error.sll"
	"map.sll"
	"path.sll"
	"string.sll"
	"sys.sll"
	"types.sll"
	"weakref.sll"
)



(# unload)



(= clib_library_type (&:
	(,,, dt
		(@@ (<- string$format (?: dt$_handle
			"<%s at %#.16llx>"
			"<%s at (unloaded)>"
		) dt$file_path dt$_handle))
	) @@string@@
	(,,, dt
		(<- unload dt)
	) @@delete@@
	(~ string_type) file_path
	int_type _handle
))

(= clib_library_module_type (&:
	(~ type_type) clib_library_type
	(~ clib_library_type) SLL_LIBRARY
	(~ func_type) load
	(~ func_type) lookup_symbol
	(~ func_type) unload
))



(= SLL_LIBRARY (. clib_library_type
	sys$LIBRARY
	(<- (... "clib:get_sll_library_handle"))
))

(= _lib_map <
	sys$LIBRARY (<- weakref$ref SLL_LIBRARY)
>)



(= load (,,, file_path
	(= lib (<- weakref$get (: _lib_map file_path)))
	(? (!== lib weakref$WEAKREF_NO_OBJECT) (@@ lib))
	(= handle (<- (... "clib:load_library")
		file_path
	))
	(? (< handle 0) (@@ (<- error$from_internal (~ handle))))
	(= lib (. clib_library_type
		file_path
		handle
	))
	(= (: _lib_map file_path) (<- weakref$ref lib))
	(@@ lib)
))

(= lookup_symbol (,,, lib
	(? (! lib) (@@ nil))
	(= lib (:: lib clib_library_type))
	(@@ nil)
))

(= unload (,,, lib
	(? (! lib) (@@ false))
	(= lib (:: lib clib_library_type))
	(? (! lib$_handle) (@@ false))
	(<- map$remove _lib_map lib$file_path)
	(= handle lib$_handle)
	(= lib$_handle 0)
	(@@ (<- (... "clib:unload_library")
		handle
	))
))



(= clib_library (. clib_library_module_type
	clib_library_type
	SLL_LIBRARY
	load
	lookup_symbol
	unload
))
(## clib_library)
