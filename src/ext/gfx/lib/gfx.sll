(---
	"gfx/_load_library.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
	"window/util.sll"
	"window/window.sll"
)



(# delete_context)



(= gfx_context_type (&:
	(,,, data
		(<- delete_context data)
	) @@delete@@
	(,,, data
		(@@ (<- string$format "<gfx window=%s>"
			(:: data$window string_type)
		))
	) @@string@@
	(~ int_type) _handle
	(~ window_window$window_window_type) window
))

(= gfx_module_type (&:
	(~ func_type) create_context
	(~ func_type) delete_context
	(~ func_type) get_context
))



(= create_context (,,, window
	(? (! window) (@@ nil))
	(= window (:: window window_window$window_window_type))
	(= ctx (<- weakref$get window$_gfx_context))
	(? (!== ctx window$WEAKREF_NO_OBJECT) (@@ ctx))
	(= handle (<- (... "gfx:context_create")
		window$id
		(<- window_util$get_context)
	))
	(= out (. gfx_context_type
		handle
		window
	))
	(= window$_gfx_context (<- weakref$ref out))
	(@@ out)
))

(= delete_context (,,, ctx
	(= ctx$window$_gfx_context weakref$NIL_REF)
	(<- (... "gfx:context_delete") ctx$_handle)
))

(= get_context (,,, window
	(? (! window) (@@ nil))
	(= window (:: window window_window$window_window_type))
	(= out (<- weakref$get window$_gfx_context))
	(@@ (?: (=== out weakref$WEAKREF_NO_OBJECT)
		nil
		out
	))
))



(= gfx (. gfx_module_type
	create_context
	delete_context
	get_context
))
(## gfx)
