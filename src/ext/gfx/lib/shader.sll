(---
	"gfx/_load_library.sll"
	"gfx/context.sll"
	"gfx/stage.sll"
	"string.sll"
	"types.sll"
)



(# delete)



(= gfx_shader_type (&:
	(,,, data
		(@@ (<- string$format "<gfx:shader type=%s entry_point=%s context=%s>"
			(<- gfx:stages$to_string data$type)
			data$entry_point
			(:: data$ctx string_type)
		))
	) @@string@@
	(,,, data
		(<- delete data)
	) @@delete@@
	(~ int_type) _handle
	(~ int_type) type
	(~ string_type) entry_point
	(~ gfx:context$gfx_context_type) ctx
))

(= gfx_shader_module_type (&:
	(~ int_type) TYPE_FRAGMENT
	(~ int_type) TYPE_GEOMETRY
	(~ int_type) TYPE_TESSELLATION_CONTROL
	(~ int_type) TYPE_TESSELLATION_EVALUATION
	(~ int_type) TYPE_VERTEX
	(~ type_type) gfx_shader_type
	(~ func_type) create
	(~ func_type) delete
))



(= create (,,, ctx bytecode type entry_point
	(? (! ctx) (@@ nil))
	(= ctx (:: ctx gfx:context$gfx_context_type))
	(= type (& (:: type int_type) gfx:stages$STAGE_ALL))
	(= entry_point (:: entry_point string_type))
	(= handle (<- (... "gfx:shader_create")
		ctx$_handle
		(:: bytecode string_type)
		type
		entry_point
	))
	(@@ (. gfx_shader_type
		handle
		type
		entry_point
		ctx
	))
))

(= delete (,,, shader
	(? (! shader) (@@))
	(= shader (:: shader gfx_shader_type))
	(<- (... "gfx:shader_delete") shader$ctx$_handle shader$_handle)
))



(= gfx:shader (. gfx_shader_module_type
	TYPE_FRAGMENT
	TYPE_GEOMETRY
	TYPE_TESSELLATION_CONTROL
	TYPE_TESSELLATION_EVALUATION
	TYPE_VERTEX
	gfx_shader_type
	create
	delete
))
(## gfx:shader)
