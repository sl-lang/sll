(---
	"gfx/_load_library.sll"
	"gfx/context.sll"
	"array.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
)



(# delete)



(= gfx_shader_type (&:
	(,,, data
		(@@ (<- string$format "<gfx:shader context=%s>"
			(:: data$ctx string_type)
		))
	) @@string@@
	(,,, data
		(<- delete data)
	) @@delete@@
	(~ int_type) _handle
	(~ gfx:context$gfx_context_type) ctx
	weakref$weakref_type _ref_object
))

(= gfx_shader_module_type (&:
	(~ func_type) create
	(~ func_type) delete
))



(= create (,,, ctx bytecode
	(? (! ctx) (@@ nil))
	(= ctx (:: ctx gfx:context$gfx_context_type))
	(= handle (<- (... "gfx:shader_create")
		ctx$_handle
		(:: bytecode string_type)
	))
	(= out (. gfx_shader_type
		handle
		ctx
	))
	(= out$_ref_object (<- weakref$ref out))
	(<- array$push ctx$shaders out$_ref_object)
	(@@ out)
))

(= delete (,,, shader
	(? (! shader) (@@))
	(= shader (:: shader gfx_shader_type))
	(<- array$remove shader$ctx$shaders shader$_ref_object)
	(<- (... "gfx:shader_delete") shader$ctx$_handle shader$_handle)
))



(= gfx:shader (. gfx_shader_module_type
	create
	delete
))
(## gfx:shader)
