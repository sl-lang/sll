(---
	"gfx/_color.sll"
	"gfx/_load_library.sll"
	"gfx/_setup_window_integration.sll"
	"string.sll"
	"time.sll"
	"types.sll"
	"weakref.sll"
	"window_manager/util.sll"
	"window_manager/window.sll"
)



(# delete)



(= gfx_context_type (&:
	(,,, data
		(@@ (<- string$format "<gfx:context window=%s clear_color=%s>"
			(:: data$window string_type)
			(:: data$clear_color string_type)
		))
	) @@string@@
	(,,, data
		(<- delete data)
	) @@delete@@
	(~ int_type) _handle
	(~ window_manager:window$window_manager_window_type) window
	gfx_color_type clear_color
	array_type buffers
	array_type pipelines
	array_type shaders
	float_type delta_time
	float_type _last_time
))

(= gfx_context_module_type (&:
	(~ type_type) gfx_context_type
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) get
	(~ func_type) render
))



(= create (,,, window enable_debug
	(? (! window) (@@ nil))
	(= window (:: window window_manager:window$window_manager_window_type))
	(= ctx (<- weakref$get_default window$_gfx_context))
	(? ctx (@@ ctx))
	(= handle (<- (... "gfx:context_create")
		window$id
		(<- window_manager:util$get_context)
		(!! enable_debug)
	))
	(= out (. gfx_context_type
		handle
		window
		[0 0 0 0]
		[]
		[]
		[]
		0
		(<- time$time)
	))
	(= window$_gfx_context (<- weakref$ref out))
	(@@ out)
))

(= delete (,,, ctx
	(? (! ctx) (@@))
	(= ctx (:: ctx gfx_context_type))
	(= ctx$window$_gfx_context weakref$NIL_REF)
	(-> (= i 0) (< i ($ ctx$buffers))
		(= buffer (<- weakref$get (: ctx$buffers i)))
		(<- (... "gfx:buffer_delete") ctx$_handle buffer$_handle)
		(++ i)
	)
	(-> (= i 0) (< i ($ ctx$pipelines))
		(= pipeline (<- weakref$get (: ctx$pipelines i)))
		(<- (... "gfx:pipeline_delete") ctx$_handle pipeline$_handle)
		(++ i)
	)
	(-> (= i 0) (< i ($ ctx$shaders))
		(= shader (<- weakref$get (: ctx$shaders i)))
		(<- (... "gfx:shader_delete") ctx$_handle shader$_handle)
		(++ i)
	)
	(<- (... "gfx:context_delete") ctx$_handle)
))

(= get (,,, window
	(? (! window) (@@ nil))
	(= window (:: window window_manager:window$window_manager_window_type))
	(@@ (<- weakref$get_default	window$_gfx_context))
))

(= render (,,, ctx
	(? (! ctx) (@@))
	(= ctx (:: ctx gfx_context_type))
	(<- (... "gfx:context_render") ctx$_handle)
	(= current (<- time$time))
	(= ctx$delta_time (- current ctx$_last_time))
	(= ctx$_last_time current)
))



(= gfx:context (. gfx_context_module_type
	gfx_context_type
	create
	delete
	get
	render
))
(## gfx:context)
