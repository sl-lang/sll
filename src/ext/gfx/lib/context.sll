(---
	"gfx/_color.sll"
	"gfx/_load_library.sll"
	"gfx/_setup_window_integration.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
	"window_manager/util.sll"
	"window_manager/window.sll"
)



(# delete)



(= gfx_context_type (&:
	(,,, data
		(<- delete data)
	) @@delete@@
	(,,, data
		(@@ (<- string$format "<gfx:context window=%s>"
			(:: data$window string_type)
		))
	) @@string@@
	(~ int_type) _handle
	(~ window_manager:window$window_manager_window_type) window
	gfx_color_type clear_color
))

(= gfx_context_module_type (&:
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) get
	(~ func_type) render
))



(= create (,,, window enable_debug
	(? (! window) (@@ nil))
	(= window (:: window window_manager:window$window_manager_window_type))
	(= ctx (<- weakref$get window$_gfx_context))
	(? (!== ctx window$WEAKREF_NO_OBJECT) (@@ ctx))
	(= handle (<- (... "gfx:context_create")
		window$id
		(<- window_manager:util$get_context)
		(!! enable_debug)
	))
	(= out (. gfx_context_type
		handle
		window
	))
	(= window$_gfx_context (<- weakref$ref out))
	(@@ out)
))

(= delete (,,, ctx
	(? (! ctx) (@@))
	(= ctx (:: ctx gfx_context_type))
	(= ctx$window$_gfx_context weakref$NIL_REF)
	(<- (... "gfx:context_delete") ctx$_handle)
))

(= get (,,, window
	(? (! window) (@@ nil))
	(= window (:: window window_manager:window$window_manager_window_type))
	(@@ (<- weakref$get_default	window$_gfx_context))
))

(= render (,,, ctx
	(? (! ctx) (@@))
	(= ctx (:: ctx gfx_context_type))
	(<- (... "gfx:context_render") ctx$_handle)
))



(= gfx:context (. gfx_context_module_type
	create
	delete
	get
	render
))
(## gfx:context)
