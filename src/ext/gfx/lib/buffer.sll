(---
	"gfx/_load_library.sll"
	"gfx/context.sll"
	"array.sll"
	"string.sll"
	"types.sll"
	"weakref.sll"
)



(# delete FREQUENCY_NEVER FREQUENCY_MEDIUM FREQUENCY_HIGH TYPE_VERTEX TYPE_INDEX TYPE_UNIFORM TYPE_ANY)



(= gfx_buffer_type (&:
	(,,, data
		(= types [])
		(?
			(=== data$type TYPE_ANY) (= types ["Any"])
			{
				(? (& data$type TYPE_VERTEX) (<- array$push types "Vertex"))
				(? (& data$type TYPE_INDEX) (<- array$push types "Index"))
				(? (& data$type TYPE_UNIFORM) (<- array$push types "Uniform"))
			}
		)
		(@@ (<- string$format "<gfx:buffer type=%s update_frequency=%s data_length=%u context=%s>"
			(<- string$join ", " types)
			(?:
				(=== data$update_frequency FREQUENCY_NEVER) "Never"
				(=== data$update_frequency FREQUENCY_MEDIUM) "Medium"
				(=== data$update_frequency FREQUENCY_HIGH) "High"
				"Low"
			)
			($ data$data)
			(:: data$ctx string_type)
		))
	) @@string@@
	(,,, data
		(<- delete data)
	) @@delete@@
	(~ int_type) _handle
	(~ int_type) type
	(~ gfx:context$gfx_context_type) ctx
	array_type data
	int_type update_frequency
	weakref$weakref_type _ref_object
))

(= gfx_buffer_module_type (&:
	(~ int_type) FREQUENCY_HIGH
	(~ int_type) FREQUENCY_LOW
	(~ int_type) FREQUENCY_MEDIUM
	(~ int_type) FREQUENCY_NEVER
	(~ int_type) TYPE_ANY
	(~ int_type) TYPE_INDEX
	(~ int_type) TYPE_UNIFORM
	(~ int_type) TYPE_VERTEX
	(~ type_type) gfx_buffer_type
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) draw
	(~ func_type) hint_update_frequency
	(~ func_type) sync
	(~ func_type) use
))



(= TYPE_VERTEX 1)
(= TYPE_INDEX 2)
(= TYPE_UNIFORM 4)
(= TYPE_ANY (| TYPE_VERTEX TYPE_INDEX TYPE_UNIFORM))

(= FREQUENCY_NEVER 0)
(= FREQUENCY_LOW 1)
(= FREQUENCY_MEDIUM 2)
(= FREQUENCY_HIGH 3)



(= create (,,, ctx type data
	(? (! ctx) (@@ nil))
	(= ctx (:: ctx gfx:context$gfx_context_type))
	(= type (& (:: type int_type) TYPE_ANY))
	(= data (?: data (:: data array_type) []))
	(= handle (<- (... "gfx:buffer_create")
		ctx$_handle
		type
	))
	(? data (<- (... "gfx:buffer_sync")
		ctx$_handle
		handle
		data
	))
	(= out (. gfx_buffer_type
		handle
		type
		ctx
		data
		FREQUENCY_LOW
	))
	(= out$_ref_object (<- weakref$ref out))
	(<- array$push ctx$buffers out$_ref_object)
	(@@ out)
))

(= delete (,,, buffer
	(? (! buffer) (@@))
	(= buffer (:: buffer gfx_buffer_type))
	(<- array$remove buffer$ctx$buffers buffer$_ref_object)
	(<- (... "gfx:buffer_delete") buffer$ctx$_handle buffer$_handle)
))

(= draw (,,, buffer
	(? (! buffer) (@@))
	(= buffer (:: buffer gfx_buffer_type))
	(<- (?: (& buffer$type TYPE_INDEX)
		(... "gfx:buffer_draw")
		(... "gfx:buffer_use")
	) buffer$ctx$_handle buffer$_handle)
))

(= hint_update_frequency (,,, buffer frequency
	(? (! buffer) (@@))
	(= buffer (:: buffer gfx_buffer_type))
	(= frequency (:: frequency int_type))
	(? (||
		(< frequency FREQUENCY_NEVER)
		(> frequency FREQUENCY_HIGH)
	) (= frequency FREQUENCY_LOW))
	(= buffer$update_frequency frequency)
	(<- (... "gfx:buffer_hint_update_frequency") buffer$ctx$_handle buffer$_handle frequency)
))

(= sync (,,, buffer
	(? (! buffer) (@@))
	(= buffer (:: buffer gfx_buffer_type))
	(<- (... "gfx:buffer_sync") buffer$ctx$_handle buffer$_handle buffer$data)
))

(= use (,,, buffer
	(? (! buffer) (@@))
	(= buffer (:: buffer gfx_buffer_type))
	(<- (... "gfx:buffer_use") buffer$ctx$_handle buffer$_handle)
))



(= gfx:buffer (. gfx_buffer_module_type
	FREQUENCY_HIGH
	FREQUENCY_LOW
	FREQUENCY_MEDIUM
	FREQUENCY_NEVER
	TYPE_ANY
	TYPE_INDEX
	TYPE_UNIFORM
	TYPE_VERTEX
	gfx_buffer_type
	create
	delete
	draw
	hint_update_frequency
	sync
	use
))
(## gfx:buffer)
