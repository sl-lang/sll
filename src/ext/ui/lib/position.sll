(---
	"string.sll"
	"types.sll"
	"ui/layout.sll"
)



(= ui_position_type (&:
	(,,, data
		(@@ (<- string$format "(%ld %ld %ld %ld)"
			data$x
			data$y
			data$width
			data$height
		))
	) @@string@@
	float_type x
	float_type y
	float_type width
	float_type height
	ui:layout$ui_layout_type x_layout
	ui:layout$ui_layout_type y_layout
	ui:layout$ui_layout_type width_layout
	ui:layout$ui_layout_type height_layout
))

(= ui_position_module_type (&:
	(~ type_type) ui_position_type
	(~ func_type) update
))



(= update (,,, position parent_position
	(? (!== (:? position) ui_position_type) (@@ false))
	(= parent_position (:: parent_position ui_position_type))
	(= prev_x position$x)
	(= prev_y position$y)
	(= prev_width position$width)
	(= prev_height position$height)
	(= position$x (<- ui:layout$solve position$x_layout parent_position$x parent_position$width))
	(= position$y (<- ui:layout$solve position$y_layout parent_position$y parent_position$height))
	(= position$width (<- ui:layout$solve position$width_layout 0 parent_position$width))
	(= position$height (<- ui:layout$solve position$height_layout 0 parent_position$height))
	(@@ (||
		(!== prev_x position$x)
		(!== prev_y position$y)
		(!== prev_width position$width)
		(!== prev_height position$height)
	))
))



(= ui:position (. ui_position_module_type
	ui_position_type
	update
))
(## ui:position)
