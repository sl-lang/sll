(---
	"string.sll"
	"types.sll"
)



(= ui_position_layout_type (&:
	(~ int_type) type
	float_type fixed
))

(= ui_position_type (&:
	(,,, data
		(@@ (<- string$format "(%ld %ld)"
			data$x
			data$y
		))
	) @@string@@
	float_type x
	float_type y
	ui_position_layout_type x_layout
	ui_position_layout_type y_layout
))

(= ui_position_module_type (&:
	(~ type_type) ui_position_layout_type
	(~ type_type) ui_position_type
	(~ int_type) LAYOUT_TYPE_FIXED
	(~ func_type) create_fixed_layout
	(~ func_type) update
))



(= LAYOUT_TYPE_FIXED 0)



(= _solve_layout (,,, layout offset size
	(?? layout$type
		LAYOUT_TYPE_FIXED (@@ (+ offset layout$fixed))
	)
	(:> "Unknown position layout: " layout '\n')
	(@@ offset)
))

(= create_fixed_layout (,,, value
	(= value (:: value float_type))
	(= out (. ui_position_layout_type
		LAYOUT_TYPE_FIXED
	))
	(= out$fixed value)
	(@@ out)
))

(= update (,,, position parent_element
	(? (!== (:? position) ui_position_type) (@@ false))
	(= prev_x position$x)
	(= prev_y position$y)
	(= position$x (<- _solve_layout position$x_layout parent_element$position$x parent_element$size$width))
	(= position$y (<- _solve_layout position$y_layout parent_element$position$y parent_element$size$height))
	(@@ (||
		(!== prev_x position$x)
		(!== prev_y position$y)
	))
))



(= ui:position (. ui_position_module_type
	ui_position_layout_type
	ui_position_type
	LAYOUT_TYPE_FIXED
	create_fixed_layout
	update
))
(## ui:position)
