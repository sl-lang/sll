(---
	"string.sll"
	"types.sll"
	"ui/layout.sll"
)



(= ui_position_type (&:
	(,,, data
		(@@ (<- string$format "(%ld %ld)"
			data$x
			data$y
		))
	) @@string@@
	float_type x
	float_type y
	ui:layout$ui_layout_type x_layout
	ui:layout$ui_layout_type y_layout
))

(= ui_position_module_type (&:
	(~ type_type) ui_position_type
	(~ ui:layout$ui_layout_type) LAYOUT_CENTER
	(~ array_type) LAYOUT_CENTER_BOTH
	(~ ui:layout$ui_layout_type) LAYOUT_INHERIT
	(~ array_type) LAYOUT_INHERIT_BOTH
	(~ func_type) update
))



(= update (,,, position parent_element
	(? (!== (:? position) ui_position_type) (@@ false))
	(= prev_x position$x)
	(= prev_y position$y)
	(= position$x (<- ui:layout$solve position$x_layout parent_element$position$x parent_element$size$width))
	(= position$y (<- ui:layout$solve position$y_layout parent_element$position$y parent_element$size$height))
	(@@ (||
		(!== prev_x position$x)
		(!== prev_y position$y)
	))
))



(= LAYOUT_CENTER (<- create_standard_layout 0.5 0))
(= LAYOUT_INHERIT (<- create_standard_layout 0 0))

(= LAYOUT_CENTER_BOTH [LAYOUT_CENTER LAYOUT_CENTER])
(= LAYOUT_INHERIT_BOTH [LAYOUT_INHERIT LAYOUT_INHERIT])



(= ui:position (. ui_position_module_type
	ui_position_type
	LAYOUT_CENTER
	LAYOUT_CENTER_BOTH
	LAYOUT_INHERIT
	LAYOUT_INHERIT_BOTH
	update
))
(## ui:position)
