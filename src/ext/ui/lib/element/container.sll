(---
	"array.sll"
	"string.sll"
	"types.sll"
	"ui/element.sll"
)



(= ui_container_element_type (&:
	(,,, data
		(@@ (<- string$format "<ui:element:container base=%s children=%s>"
			(:: data$base string_type)
			(<- string$str data$children)
		))
	) @@string@@
	ui:element$ui_element_type base
	array_type children
))

(= ui_container_element_module_type (&:
	(~ type_type) ui_container_element_type
	(~ func_type) create
	(~ func_type) delete
))



(= _container_callbacks (. ui:element$ui_element_callbacks_type
	(,,, container child
		(<- array$push container$children child)
		(@@ true)
	)
	(,,, container
		(@@ container$children)
	)
	(,,, container child
		(<- array$remove container$children child)
	)
))



(= create (,,, ui position size parent
	(= out (. ui_container_element_type
		(<- ui:element$create ui _container_callbacks position size)
		[]
	))
	(? parent (<- ui:element$set_parent out parent))
	(@@ out)
))

(= delete (,,, container
	(? (! container) (@@))
	(= container (:: container ui_container_element_type))
	(<- ui:element$delete container$base)
))



(= ui:element:container (. ui_container_element_module_type
	ui_container_element_type
	create
	delete
))
(## ui:element:container)
