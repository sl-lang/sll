(---
	"font/rendered_text.sll"
	"font/true_type.sll"
	"string.sll"
	"types.sll"
	"ui/_util.sll"
	"ui/core.sll"
	"ui/element.sll"
)



(# delete set_font set_text set_parent)



(= ui_text_element_type (&:
	(,,, data
		(@@ (<- string$format "<ui:element:text text=%s base=%s>"
			data$text
			(:: data$base string_type)
		))
	) @@string@@
	(,,, data
		(<- delete data)
	) @@delete@@
	(~ ui:element$ui_element_type) base
	string_type text
	object_type _font
	font:rendered_text$font_rendered_text_type _rendered_text
	float_type _last_x
	float_type _last_y
	float_type _last_width
	float_type _last_height
))

(= ui_element_text_module_type (&:
	(~ type_type) ui_text_element_type
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) set_font
	(~ func_type) set_text
	(~ func_type) set_parent
))



(= _create_text (,,, ui base parent font text
	(= out (. ui_text_element_type
		base
		""
		nil
		(<- font:rendered_text$create_rendered_text ui$context)
	))
	(= out$_rendered_text$color_z_coord -1.0)
	(? font (<- set_font out font))
	(? text (<- set_text out text))
	(? parent (<- set_parent out parent))
	(= out$_last_x out$base$position$x)
	(= out$_last_y out$base$position$y)
	(= out$_last_width out$base$position$width)
	(= out$_last_height out$base$position$height)
	(<- ui:element$setup out)
	(@@ out)
))

(= _deseralize_data (,,, ui base parent data
	(@@ (<- _create_text ui base parent (: data 0) (: data 1)))
))

(= _render_text (,,, text
	(<- font:rendered_text$draw_rendered_text text$_rendered_text)
))

(= _seralize_data (,,, text
	(@@ [
		text$_font$font
		text$text
	])
))

(= _update_position_and_size (,,, text
	(= text$_rendered_text$x text$base$position$x)
	(= text$_rendered_text$y text$base$position$y)
	(= text$_rendered_text$width text$base$position$width)
	(= text$_rendered_text$height text$base$position$height)
	(? (||
		(!== text$_last_x text$base$position$x)
		(!== text$_last_y text$base$position$y)
	) (<- font:rendered_text$update_rendered_text_position text$_rendered_text))
	(? (||
		(!== text$_last_width text$base$position$width)
		(!== text$_last_height text$base$position$height)
	) (<- font:rendered_text$update_rendered_text_size text$_rendered_text))
	(= text$_last_x text$base$position$x)
	(= text$_last_y text$base$position$y)
	(= text$_last_width text$base$position$width)
	(= text$_last_height text$base$position$height)
))

(= create (,,, ui position_layout parent font text
	(= ui (?: ui (:: ui ui:core$ui_type) nil))
	(@@ (<- _create_text
		ui
		(<- ui:element$create ui position_layout)
		parent
		font
		text
	))
))

(= delete (,,, text
	(? (! text) (@@))
	(= text (:: text ui_text_element_type))
	(<- ui:element$delete text)
))

(= set_font (,,, text font
	(? (! text) (@@))
	(= text (:: text ui_text_element_type))
	(= font (:: font font:true_type$font_type))
	(= text$_font (<- font:true_type$add_font text$base$ui$_font_atlas font))
	(<- font:rendered_text$update_rendered_text_font text$_rendered_text text$_font)
))

(= set_text (,,, text content
	(? (! text) (@@))
	(= text (:: text ui_text_element_type))
	(= content (: (:: content string_type)))
	(= text$text content)
	(<- font:rendered_text$update_rendered_text text$_rendered_text content)
))

(= set_parent (,,, text parent
	(? (! text) (@@))
	(= text (:: text ui_text_element_type))
	(? (! (<- ui:element$set_parent text parent)) (@@ false))
	(= text$_rendered_text$z_coord text$base$z_index)
	(<- font:rendered_text$update_rendered_text_z_coords text$_rendered_text)
	(@@ true)
))



(<- ui:element$register_element ui_text_element_type "ui:element:text" (. ui:element$ui_element_data_callbacks_type
	nil
	_deseralize_data
	nil
	nil
	_render_text
	_seralize_data
	_update_position_and_size
))



(= ui:element:text (. ui_element_text_module_type
	ui_text_element_type
	create
	delete
	set_text
	set_parent
))
(## ui:element:text)
