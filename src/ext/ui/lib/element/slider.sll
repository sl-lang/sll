(---
	"array.sll"
	"math.sll"
	"string.sll"
	"types.sll"
	"ui/element.sll"
	"ui/layout.sll"
	"ui/position.sll"
	"window_manager/window.sll"
)



(= ui_slider_element_type (&:
	(,,, data
		(@@ (<- string$format "<ui:element:slider base=%s>"
			(:: data$base string_type)
		))
	) @@string@@
	(~ ui:element$ui_element_type) base
	ui:position$ui_position_type range
	float_type x_steps
	float_type y_steps
	func_type callback
	float_type x
	float_type y
	int_type _has_mouse_focus
))

(= ui_slider_element_module_type (&:
	(~ type_type) ui_slider_element_type
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) get_position
	(~ func_type) set_position
	(~ func_type) set_slider_element
))



(= _update_handle_position (,,, slider x y
	(= x (- x slider$range$x))
	(= y (- y slider$range$y))
	(? (< x 0) (= x 0))
	(? (< y 0) (= y 0))
	(? (> slider$x_steps 1) {
		(= scale (/ slider$range$width (- slider$x_steps 1)))
		(= x (* (<- math$round (/ x scale)) scale))
	})
	(? (> slider$y_steps 1) {
		(= scale (/ slider$range$height (- slider$y_steps 1)))
		(= y (* (<- math$round (/ y scale)) scale))
	})
	(? (>= x slider$range$width) (= x slider$range$width))
	(? (>= y slider$range$height) (= y slider$range$height))
	(= screen_x (+ x slider$range$x))
	(= screen_y (+ y slider$range$y))
	(? (&& (=== screen_x slider$x) (=== screen_y slider$y)) (@@))
	(= slider$x screen_x)
	(= slider$y screen_y)
	(<- ui:element$update (: slider$base$children 0))
	(? (> slider$x_steps 1) (= x (// (* x slider$x_steps) slider$base$position$width)))
	(? (> slider$y_steps 1) (= y (// (* y slider$y_steps) slider$base$position$height)))
	;;; FIXME: Currently due to an unknown bug the following line needs to be duplicated
	(<- slider$callback slider x y)
	(<- slider$callback slider x y)
))

(= _handle_button_event (,,, slider mouse_button state
	(? (!== mouse_button window_manager:window$FLAG_BUTTON_LEFT) (@@))
	(= x slider$base$ui$window$inputs$mouse$x)
	(= y slider$base$ui$window$inputs$mouse$y)
	(= slider$_has_mouse_focus (&&
		state
		(>= x slider$base$position$x)
		(>= y slider$base$position$y)
		(< x (+ slider$base$position$x slider$base$position$width))
		(< y (+ slider$base$position$y slider$base$position$height))
	))
	(? slider$_has_mouse_focus (<- _update_handle_position slider x y))
))

(= _deseralize_data (,,, ui base parent data
	(= out (. ui_slider_element_type
		base
		[
			0
			0
			0
			0
			(<- ui:layout$deserialize (: data 0))
			(<- ui:layout$deserialize (: data 1))
			(<- ui:layout$deserialize (: data 2))
			(<- ui:layout$deserialize (: data 3))
		]
		(: data 4)
		(: data 5)
		nil
		0.0
		0.0
		0
	))
	(? parent (<- ui:element$set_parent out parent))
	(<- ui:element$register_event_handlers out)
	(<- ui:position$update out$range out$base$position)
	(= out$x out$base$position$x)
	(= out$y out$base$position$y)
	(@@ out)
))

(= _get_position (,,, slider element
	(@@ (?: (=== (%% element) (%% (: slider$base$children 0)))
		[slider$x slider$y slider$base$position$width slider$base$position$height]
		slider$base$position
	))
))

(= _handle_mouse_event (,,, slider x y
	(? slider$_has_mouse_focus (<- _update_handle_position slider x y))
))

(= _serialize_data (,,, slider
	(@@ [
		(<- ui:layout$serialize slider$range$x_layout)
		(<- ui:layout$serialize slider$range$y_layout)
		(<- ui:layout$serialize slider$range$width_layout)
		(<- ui:layout$serialize slider$range$height_layout)
		slider$x_steps
		slider$y_steps
	])
))

(= create (,,, ui position_layout parent range x_steps y_steps callback
	(= range (:: range array_type))
	(= x_steps (:: x_steps int_type))
	(= y_steps (:: y_steps int_type))
	(= callback (:: callback func_type))
	(= out (. ui_slider_element_type
		(<- ui:element$create ui position_layout)
		[
			0
			0
			0
			0
			(: range 0)
			(: range 1)
			(: range 2)
			(: range 3)
		]
		x_steps
		y_steps
		callback
		0.0
		0.0
		0
	))
	(? parent (<- ui:element$set_parent out parent))
	(<- ui:element$register_event_handlers out)
	(<- ui:position$update out$range out$base$position)
	(= out$x out$base$position$x)
	(= out$y out$base$position$y)
	(@@ out)
))

(= delete (,,, slider
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(<- ui:element$delete slider)
))

(= get_position (,,, slider
	(? (! slider) (@@ [0 0]))
	(= slider (:: slider ui_slider_element_type))
	(= x (- slider$x slider$range$x))
	(= y (- slider$y slider$range$y))
	(@@ [
		(?: (> slider$x_steps 1)
			(<- math$round (/ (* x slider$x_steps) slider$base$position$width))
			x
		)
		(?: (> slider$y_steps 1)
			(<- math$round (/ (* y slider$y_steps) slider$base$position$height))
			y
		)
	])
))

(= set_position (,,, slider x y
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(= x (:: x float_type))
	(= y (:: y float_type))
	(? (> slider$x_steps 1) (= x (/ (* x slider$base$position$width) slider$x_steps)))
	(? (> slider$y_steps 1) (= y (/ (* y slider$base$position$width) slider$y_steps)))
	(<- _update_handle_position slider
		(+ slider$base$position$x x)
		(+ slider$base$position$y y)
	)
))

(= set_slider_element (,,, slider element
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(? (||
		(! (<- ui:element$is_valid_element element))
		(!== (%% element$base$parent) (%% slider))
	) (@@))
	(-> (= i 0) (< i ($ slider$base$children))
		(? (=== (%% (: slider$base$children i)) (%% element)) {
			(= temp (: slider$base$children 0))
			(= (: slider$base$children 0) element)
			(= (: slider$base$children i) temp)
			(<- ui:element$update temp)
			(<- ui:element$update element)
			(@)
		})
		(++ i)
	)
))



(<- ui:element$register_element ui_slider_element_type "ui:element:slider" (. ui:element$ui_element_callbacks_type
	_handle_button_event
	_deseralize_data
	_get_position
	_handle_mouse_event
	_serialize_data
	_update_position_and_size
))



(= ui:element:slider (. ui_slider_element_module_type
	ui_slider_element_type
	create
	delete
	get_position
	set_position
	set_slider_element
))
(## ui:element:slider)
