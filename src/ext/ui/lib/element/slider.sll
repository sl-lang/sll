(---
	"array.sll"
	"string.sll"
	"types.sll"
	"ui/element.sll"
	"ui/position.sll"
	"window_manager/window.sll"
)



(= ui_slider_element_type (&:
	(,,, data
		(@@ (<- string$format "<ui:element:slider base=%s>"
			(:: data$base string_type)
		))
	) @@string@@
	(~ ui:element$ui_element_type) base
	float_type x
	float_type y
	ui:position$ui_position_type range
))

(= ui_slider_element_module_type (&:
	(~ type_type) ui_slider_element_type
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) set_slider_element
))



(= _update_handle_position (,,, slider x y
	(? (< x slider$range$x) (= x slider$range$x))
	(? (< y slider$range$y) (= y slider$range$y))
	(= x2 (+ slider$range$x slider$range$width))
	(= y2 (+ slider$range$y slider$range$height))
	(? (>= x x2) (= x x2))
	(? (>= y y2) (= y y2))
	(? (&& (=== x slider$x) (=== y slider$y)) (@@))
	(= slider$x x)
	(= slider$y y)
	(<- ui:element$update (: slider$base$children 0))
))

(= _handle_button_event (,,, slider mouse_button state
	(= x slider$base$ui$window$inputs$mouse$x)
	(= y slider$base$ui$window$inputs$mouse$y)
	(? (||
		(!== mouse_button window_manager:window$FLAG_BUTTON_LEFT)
		(! state)
		(< x slider$base$position$x)
		(< y slider$base$position$y)
		(>= x (+ slider$base$position$x slider$base$position$width))
		(>= y (+ slider$base$position$y slider$base$position$height))
	) (@@))
	(<- _update_handle_position slider x y)
))

(= _get_position (,,, slider element
	(@@ (?: (=== (%% element) (%% (: slider$base$children 0)))
		[slider$x slider$y slider$base$position$width slider$base$position$height]
		slider$base$position
	))
))

(= _update_position_and_size (,,, slider
	(<- ui:position$update slider$range slider$base$position)
	(<- _update_handle_position slider slider$x slider$y)
))

(= create (,,, ui position_layout parent range
	(= range (:: range array_type))
	(= out (. ui_slider_element_type
		(<- ui:element$create ui position_layout)
		0.0
		0.0
		[
			0
			0
			0
			0
			(: range 0)
			(: range 1)
			(: range 2)
			(: range 3)
		]
	))
	(? parent (<- ui:element$set_parent out parent))
	(<- ui:element$register_event_handlers out)
	(<- _update_position_and_size out)
	(@@ out)
))

(= delete (,,, slider
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(<- ui:element$delete slider)
))

(= set_slider_element (,,, slider element
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(? (||
		(! (<- ui:element$is_valid_element element))
		(!== (%% element$base$parent) (%% slider))
	) (@@))
	(-> (= i 0) (< i ($ slider$base$children))
		(? (=== (%% (: slider$base$children i)) (%% element)) {
			(= temp (: slider$base$children 0))
			(= (: slider$base$children 0) element)
			(= (: slider$base$children i) temp)
			(<- ui:element$update temp)
			(<- ui:element$update element)
			(@)
		})
		(++ i)
	)
))



(<- ui:element$register_element ui_slider_element_type "ui:element:slider" (. ui:element$ui_element_callbacks_type
	_handle_button_event
	nil
	_get_position
	nil
	_update_position_and_size
))



(= ui:element:slider (. ui_slider_element_module_type
	ui_slider_element_type
	create
	delete
	set_slider_element
))
(## ui:element:slider)
