(---
	"array.sll"
	"string.sll"
	"types.sll"
	"ui/element.sll"
)



(= ui_slider_element_type (&:
	(,,, data
		(@@ (<- string$format "<ui:element:slider base=%s>"
			(:: data$base string_type)
		))
	) @@string@@
	(~ ui:element$ui_element_type) base
	float_type x
	float_type y
))

(= ui_slider_element_module_type (&:
	(~ type_type) ui_slider_element_type
	(~ func_type) create
	(~ func_type) delete
	(~ func_type) set_slider_element
))



(= _get_position (,,, slider element
	(@@ (?: (=== (%% element) (%% (: slider$base$children 0)))
		[slider$x slider$y slider$base$position$width slider$base$position$height]
		slider$base$position
	))
))

(= _update_position_and_size (,,, slider
	(= new_x slider$x)
	(= new_y slider$y)
	(? (< new_x slider$base$position$x) (= new_x slider$base$position$x))
	(? (< new_y slider$base$position$y) (= new_y slider$base$position$y))
	(= x2 (+ slider$base$position$x slider$base$position$width))
	(= y2 (+ slider$base$position$y slider$base$position$height))
	(? (>= new_x x2) (= new_x x2))
	(? (>= new_y y2) (= new_y y2))
	(= update? (|| (!== new_x slider$x) (!== new_y slider$y)))
	(= slider$x new_x)
	(= slider$y new_y)
	(? update? (<- ui:element$update (: slider$base$children 0)))
))

(= create (,,, ui position_layout parent
	(= out (. ui_slider_element_type
		(<- ui:element$create ui position_layout)
		0.0
		0.0
	))
	(? parent (<- ui:element$set_parent out parent))
	(@@ out)
))

(= delete (,,, slider
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(<- ui:element$delete slider)
))

(= set_slider_element (,,, slider element
	(? (! slider) (@@))
	(= slider (:: slider ui_slider_element_type))
	(? (||
		(! (<- ui:element$is_valid_element element))
		(!== (%% element$base$parent) (%% slider))
	) (@@))
	(-> (= i 0) (< i ($ slider$base$children))
		(? (=== (%% (: slider$base$children i)) (%% element)) {
			(= temp (: slider$base$children 0))
			(= (: slider$base$children 0) element)
			(= (: slider$base$children i) temp)
			(<- ui:element$update temp)
			(<- ui:element$update element)
			(@)
		})
		(++ i)
	)
))



(<- ui:element$register_element ui_slider_element_type "ui:element:slider" (. ui:element$ui_element_callbacks_type
	nil
	nil
	_get_position
	nil
	_update_position_and_size
))



(= ui:element:slider (. ui_slider_element_module_type
	ui_slider_element_type
	create
	delete
	set_slider_element
))
(## ui:element:slider)
