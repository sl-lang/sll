(---
	"array.sll"
	"gfx/buffer.sll"
	"gfx/sampler.sll"
	"gfx/texture.sll"
	"string.sll"
	"types.sll"
	"ui/core.sll"
	"ui/shader/_texture_fragment_shader.sll"
	"ui/shader/_texture_vertex_shader.sll"
)



(= ui_texture_type (&:
	(,,, data
		(@@ (<- string$format "<ui:texture texture=%s sampler=%s>"
			(:: data$texture string_type)
			(:: data$sampler string_type)
		))
	) @@string@@
	(~ ui:core$ui_type) ui
	gfx:texture$gfx_texture_type texture
	gfx:sampler$gfx_sampler_type sampler
	gfx:buffer$gfx_buffer_type _vertex_buffer
	gfx:buffer$gfx_buffer_type _index_buffer
	int_type _buffer_update_flags
))

(= ui_texture_module_type (&:
	(~ type_type) ui_texture_type
	(~ func_type) create
))



(= create (,,, ui texture sampler
	(? (! ui) (@@ nil))
	(= ui (:: ui ui:core$ui_type))
	(= texture (:: texture gfx:texture$gfx_texture_type))
	(= sampler (:: sampler gfx:sampler$gfx_sampler_type))
	(= vertex_buffer (<- gfx:buffer$create ui$context gfx:buffer$TYPE_VERTEX gfx:buffer$DATA_TYPE_FLOAT32))
	(<- gfx:buffer$hint_update_frequency vertex_buffer gfx:buffer$FREQUENCY_LOW)
	(= index_buffer (<- gfx:buffer$create ui$context gfx:buffer$TYPE_INDEX gfx:buffer$DATA_TYPE_UINT16))
	(<- gfx:buffer$hint_update_frequency index_buffer gfx:buffer$FREQUENCY_LOW)
	(= out (. ui_texture_type
		ui
		texture
		sampler
		vertex_buffer
		index_buffer
		0
	))
	(<- array$push ui$_textures out)
	(@@ out)
))



(= ui:texture (. ui_texture_module_type
	ui_texture_type
	create
))
(## ui:texture)
