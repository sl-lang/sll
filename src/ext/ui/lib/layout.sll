(---
	"math.sll"
	"string.sll"
	"types.sll"
)



(# _type_to_string)



(= ui_layout_standard_type (&:
	float_type multiplier
	float_type bias
))

(= ui_layout_filter_type (&:
	int_type type
	array_type values
))

(= ui_layout_range_type (&:
 	object_type min
 	object_type value
 	object_type max
))

(= ui_layout_type (&:
	(,,, data
		(@@ (<- string$format "<ui:layout type=%s%s>"
			(: _type_to_string data$type)
			(:: (: data (: _type_to_string data$type)) string_type)
		))
	) @@string@@
	(~ int_type) type
	ui_layout_standard_type standard
	ui_layout_filter_type filter
	ui_layout_range_type range
))

(= ui_layout_module_type (&:
	(~ type_type) ui_layout_type
	(~ int_type) FILTER_AVERAGE
	(~ int_type) FILTER_GEOMETRIC_AVERAGE
	(~ int_type) FILTER_HIGHEST
	(~ int_type) FILTER_LOWEST
	(~ int_type) TYPE_FILTER
	(~ int_type) TYPE_RANGE
	(~ int_type) TYPE_STANDARD
	(~ func_type) create_filter_layout
	(~ func_type) create_range_layout
	(~ func_type) create_standard_layout
	(~ func_type) deserialize
	(~ func_type) serialize
	(~ func_type) solve
))



(= TYPE_STANDARD 0)
(= TYPE_FILTER 1)
(= TYPE_RANGE 2)

(= FILTER_LOWEST 0)
(= FILTER_HIGHEST 1)
(= FILTER_AVERAGE 2)
(= FILTER_GEOMETRIC_AVERAGE 3)

(= _type_to_string <
	TYPE_STANDARD "standard"
	TYPE_FILTER "filter"
	TYPE_RANGE "range"
>)

(= _string_to_type <
	"standard" TYPE_STANDARD
	"filter" TYPE_FILTER
	"range" TYPE_RANGE
>)

(= _filter_type_to_string <
	FILTER_LOWEST "lowest"
	FILTER_HIGHEST "highest"
	FILTER_AVERAGE "average"
	FILTER_GEOMETRIC_AVERAGE "geometric_average"
>)

(= _string_to_filter_type <
	"lowest" FILTER_LOWEST
	"highest" FILTER_HIGHEST
	"average" FILTER_AVERAGE
	"geometric_average" FILTER_GEOMETRIC_AVERAGE
>)



(= create_filter_layout (,,, type values
	(= type (:: type int_type))
	(= values (:: values array_type))
	(= out (. ui_layout_type
		TYPE_FILTER
	))
	(= out$filter (. ui_layout_filter_type
		type
		values
	))
	(@@ out)
))

(= create_range_layout (,,, min value max
	(= min (:: min ui_layout_type))
	(= value (:: value ui_layout_type))
	(= max (:: max ui_layout_type))
	(= out (. ui_layout_type
		TYPE_RANGE
	))
	(= out$range (. ui_layout_range_type
		min
		value
		max
	))
	(@@ out)
))

(= create_standard_layout (,,, multiplier bias
	(= multiplier (:: multiplier float_type))
	(= bias (:: bias float_type))
	(= out (. ui_layout_type
		TYPE_STANDARD
	))
	(= out$standard$multiplier multiplier)
	(= out$standard$bias bias)
	(@@ out)
))

(= deserialize (,,, data
	(= data (:: data map_type))
	(= type (: _string_to_type data$type))
	(= out (. ui_layout_type
		type
	))
	(?? type
		TYPE_STANDARD {
			(= out$standard$multiplier data$multiplier)
			(= out$standard$bias data$bias)
		}
		TYPE_FILTER {
			(= out$filter$type (: _string_to_filter_type data$filter))
			(= out$filter$values ([> (= i 0) (< i ($ data$values))
				(= value (: data$values i))
				(++ i)
				(<- deserialize value)
			))
		}
		TYPE_RANGE {
			(= out$range$min (<- deserialize data$min))
			(= out$range$value (<- deserialize data$value))
			(= out$range$max (<- deserialize data$max))
		}
	)
	(@@ out)
))

(= serialize (,,, layout
	(? (! layout) (@@ nil))
	(= layout (:: layout ui_layout_type))
	(= out <
		"type" (: _type_to_string layout$type)
	>)
	(?? layout$type
		TYPE_STANDARD {
			(? layout$standard$multiplier (= out$multiplier layout$standard$multiplier))
			(? layout$standard$bias (= out$bias layout$standard$bias))
		}
		TYPE_FILTER {
			(= out$filter (: _filter_type_to_string layout$filter$type))
			(= out$values ([> (= i 0) (< i ($ layout$filter$values))
				(= value (: layout$filter$values i))
				(++ i)
				(<- serialize value)
			))
		}
		TYPE_RANGE {
			(= out$min (<- serialize layout$range$min))
			(= out$value (<- serialize layout$range$value))
			(= out$max (<- serialize layout$range$max))
		}
	)
	(@@ out)
))

(= solve (,,, layout offset size
	(?? layout$type
		TYPE_STANDARD (@@ (+ offset (* layout$standard$multiplier size) layout$standard$bias))
		TYPE_FILTER {
			(= out nil)
			(-> (= i 0) (< i ($ layout$filter$values))
				(= value (<- solve (: layout$filter$values i) offset size))
				(?
					(||
						(! i)
						(&& (=== layout$filter$type FILTER_LOWEST) (< value out))
						(&& (=== layout$filter$type FILTER_HIGHEST) (> value out))
					) (= out value)
					(=== layout$filter$type FILTER_AVERAGE) (= out (+ out value))
					(=== layout$filter$type FILTER_GEOMETRIC_AVERAGE) (= out (* out value))
				)
				(++ i)
			)
			(? layout$filter$values (?? layout$filter$type
				FILTER_AVERAGE (= out (/ out ($ layout$filter$values)))
				FILTER_GEOMETRIC_AVERAGE (= out (<- math$pow out (/ 1.0 ($ layout$filter$values))))
			))
			(@@ out)
		}
		TYPE_RANGE {
			(= min (<- solve (: layout$range$min) offset size))
			(= value (<- solve (: layout$range$value) offset size))
			(= max (<- solve (: layout$range$max) offset size))
			(? (< max min) {
				(= temp max)
				(= max min)
				(= min temp)
			})
			(? (< value min) (= value min))
			(? (> value max) (= value max))
			(@@ value)
		}
	)
	(:> "Unknown layout: " layout '\n')
	(@@ (+ offset (/ size 2)))
))



(= ui:layout (. ui_layout_module_type
	ui_layout_type
	FILTER_AVERAGE
	FILTER_GEOMETRIC_AVERAGE
	FILTER_HIGHEST
	FILTER_LOWEST
	TYPE_FILTER
	TYPE_RANGE
	TYPE_STANDARD
	create_filter_layout
	create_range_layout
	create_standard_layout
	deserialize
	serialize
	solve
))
(## ui:layout)
