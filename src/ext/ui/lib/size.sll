(---
	"string.sll"
	"types.sll"
	"ui/layout.sll"
)



(= ui_size_type (&:
	(,,, data
		(@@ (<- string$format "(%ld %ld)"
			data$width
			data$height
		))
	) @@string@@
	float_type width
	float_type height
	ui:layout$ui_layout_type width_layout
	ui:layout$ui_layout_type height_layout
))

(= ui_size_module_type (&:
	(~ type_type) ui_size_type
	(~ ui:layout$ui_layout_type) LAYOUT_HALF
	(~ array_type) LAYOUT_HALF_BOTH
	(~ ui:layout$ui_layout_type) LAYOUT_INHERIT
	(~ array_type) LAYOUT_INHERIT_BOTH
	(~ func_type) update
))



(= update (,,, size parent_element
	(? (!== (:? size) ui_size_type) (@@ false))
	(= prev_width size$width)
	(= prev_height size$height)
	(= size$width (<- ui:layout$solve size$width_layout 0 parent_element$size$width))
	(= size$height (<- ui:layout$solve size$height_layout 0 parent_element$size$height))
	(@@ (||
		(!== prev_width size$width)
		(!== prev_height size$height)
	))
))



(= LAYOUT_HALF (<- ui:layout$create_standard_layout 0.5 0))
(= LAYOUT_INHERIT (<- ui:layout$create_standard_layout 1 0))

(= LAYOUT_HALF_BOTH [LAYOUT_HALF LAYOUT_HALF])
(= LAYOUT_INHERIT_BOTH [LAYOUT_INHERIT LAYOUT_INHERIT])



(= ui:size (. ui_size_module_type
	ui_size_type
	LAYOUT_HALF
	LAYOUT_HALF_BOTH
	LAYOUT_INHERIT
	LAYOUT_INHERIT_BOTH
	update
))
(## ui:size)
