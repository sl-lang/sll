(---
	"array.sll"
	"ds4/_load_library.sll"
	"file.sll"
	"string.sll"
	"types.sll"
)



(# BUTTON_UP BUTTON_DOWN BUTTON_LEFT BUTTON_RIGHT BUTTON_L1 BUTTON_R1 BUTTON_L2 BUTTON_R2 BUTTON_L3 BUTTON_R3 BUTTON_CROSS BUTTON_CIRCLE BUTTON_SQURARE BUTTON_TRIANGLE BUTTON_OPTIONS BUTTON_SHARE BUTTON_LOGO BUTTON_TOUCHPAD delete)



(= ds4_device_acceleration_type (&:
	(,,, data
		(@@ (<- string$format "(%f %f %f)"
			data$x
			data$y
			data$z
		))
	) @@string@@
	float_type x
	float_type y
	float_type z
))

(= ds4_device_angular_velocity_type (&:
	(,,, data
		(@@ (<- string$format "(%f %f %f)"
			data$x
			data$y
			data$z
		))
	) @@string@@
	float_type x
	float_type y
	float_type z
))

(= ds4_device_touch_point_type (&:
	(,,, data
		(@@ (<- string$format "{%u}(%u %u)"
			data$id
			data$x
			data$y
		))
	) @@string@@
	int_type id
	int_type x
	int_type y
))

(= ds4_device_controls_type (&:
	(,,, data
		(@@ (<- string$format "<ds4:device_controls color=%0.2x%0.2x%0.2x small_rumble=%u big_rumble=%u led-timer=(%u %u)>"
			data$red
			data$green
			data$blue
			data$small_rumble
			data$big_rumble
			data$led_on
			data$led_off
		))
	) @@string@@
	int_type red
	int_type green
	int_type blue
	int_type small_rumble
	int_type big_rumble
	int_type led_on
	int_type led_off
))

(= ds4_device_type (&:
	(,,, data
		(= buttons [])
		(? (& data$buttons BUTTON_UP) (<- array$push buttons "Up"))
		(? (& data$buttons BUTTON_DOWN) (<- array$push buttons "Down"))
		(? (& data$buttons BUTTON_LEFT) (<- array$push buttons "Left"))
		(? (& data$buttons BUTTON_RIGHT) (<- array$push buttons "Right"))
		(? (& data$buttons BUTTON_L1) (<- array$push buttons "L1"))
		(? (& data$buttons BUTTON_R1) (<- array$push buttons "R1"))
		(? (& data$buttons BUTTON_L2) (<- array$push buttons "L2"))
		(? (& data$buttons BUTTON_R2) (<- array$push buttons "R2"))
		(? (& data$buttons BUTTON_L3) (<- array$push buttons "L3"))
		(? (& data$buttons BUTTON_R3) (<- array$push buttons "R3"))
		(? (& data$buttons BUTTON_CROSS) (<- array$push buttons "Cross"))
		(? (& data$buttons BUTTON_CIRCLE) (<- array$push buttons "Circle"))
		(? (& data$buttons BUTTON_SQURARE) (<- array$push buttons "Squrare"))
		(? (& data$buttons BUTTON_TRIANGLE) (<- array$push buttons "Triangle"))
		(? (& data$buttons BUTTON_OPTIONS) (<- array$push buttons "Options"))
		(? (& data$buttons BUTTON_SHARE) (<- array$push buttons "Share"))
		(? (& data$buttons BUTTON_LOGO) (<- array$push buttons "Logo"))
		(? (& data$buttons BUTTON_TOUCHPAD) (<- array$push buttons "Touchpad"))
		(@@ (<- string$format "<ds4:device path=%s buttons=%s left=(%d %d) right=(%d %d) left-trigger=%u right-trigger=%u battery=%s acceleration=%s angular-velocity=%s touchpad=%s controls=%s>"
			data$path
			(<- string$join ", " buttons)
			data$left-x
			data$left-y
			data$right-x
			data$right-y
			data$left-trigger
			data$right-trigger
			(|| data$battery "Charging")
			(:: data$acceleration string_type)
			(:: data$angular_velocity string_type)
			(<- string$str data$touchpad)
			(:: data$controls string_type)
		))
	) @@string@@
	(,,, data
		(<- delete data)
	) @@delete@@
	(~ string_type) path
	int_type buttons
	int_type left-x
	int_type left-y
	int_type right-x
	int_type right-y
	int_type left-trigger
	int_type right-trigger
	int_type battery
	ds4_device_acceleration_type acceleration
	ds4_device_angular_velocity_type angular_velocity
	array_type touchpad
	ds4_device_controls_type controls
	(~ file$file_type) _file
	string_type _data_buffer
))

(= ds4_device_module_type (&:
	(~ type_type) ds4_device_controls_type
	(~ type_type) ds4_device_touch_point_type
	(~ type_type) ds4_device_type
	(~ int_type) BATTERY_CHARGING
	(~ int_type) BUTTON_CIRCLE
	(~ int_type) BUTTON_CROSS
	(~ int_type) BUTTON_DOWN
	(~ int_type) BUTTON_L1
	(~ int_type) BUTTON_L2
	(~ int_type) BUTTON_L3
	(~ int_type) BUTTON_LEFT
	(~ int_type) BUTTON_LOGO
	(~ int_type) BUTTON_OPTIONS
	(~ int_type) BUTTON_R1
	(~ int_type) BUTTON_R2
	(~ int_type) BUTTON_R3
	(~ int_type) BUTTON_RIGHT
	(~ int_type) BUTTON_SHARE
	(~ int_type) BUTTON_SQURARE
	(~ int_type) BUTTON_TOUCHPAD
	(~ int_type) BUTTON_TRIANGLE
	(~ int_type) BUTTON_UP
	(~ func_type) create
	(~ func_type) list
	(~ func_type) poll
	(~ func_type) update
))



(= ACCELERATION_FACTOR 0.0011733020388230986)
(= ANGULAR_VELOCITY_FACTOR 0.0010652969463144809)

(= BUTTON_UP 1)
(= BUTTON_DOWN 2)
(= BUTTON_LEFT 4)
(= BUTTON_RIGHT 8)
(= BUTTON_L1 16)
(= BUTTON_R1 32)
(= BUTTON_L2 64)
(= BUTTON_R2 128)
(= BUTTON_L3 256)
(= BUTTON_R3 512)
(= BUTTON_CROSS 1024)
(= BUTTON_CIRCLE 2048)
(= BUTTON_SQURARE 4096)
(= BUTTON_TRIANGLE 8192)
(= BUTTON_OPTIONS 16384)
(= BUTTON_SHARE 32768)
(= BUTTON_LOGO 65536)
(= BUTTON_TOUCHPAD 131072)

(= BATTERY_CHARGING 0)



(= _decode_imu_data (,,, data offset
	(= value (|
		(: data offset)
		(<< (: data (+ offset 1)) 8)
	))
	(? (& value 0x8000) (= value (~ (- 0xffff value))))
	(@@ (:: value float_type))
))

(= _decode_touchpad_point (,,, data offset
	(@@ (. ds4_device_touch_point_type
		(: data offset)
		(|
			(: data (+ offset 1))
			(<< (& (: data (+ offset 2)) 15) 8)
		)
		(|
			(>> (: data (+ offset 2)) 4)
			(<< (: data (+ offset 3)) 4)
		)
	))
))

(= create (,,, path
	(= path (:: path string_type))
	(= handle (<- file$open path (| file$FLAG_READ file$FLAG_WRITE file$FLAG_NO_BUFFER)))
	(? (!== (:? handle) file$file_type) (@@ handle))
	(@@ (. ds4_device_type
		path
		0
		0
		0
		0
		0
		0
		0
		0
		[0.0 0.0 0.0]
		[0.0 0.0 0.0]
		[]
		[
			0
			0
			0
			0
			0
			0
			0
		]
		handle
		"\x05\x7f\x04\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00"
	))
))

(= delete (,,, device
	(? (! device) (@@ false))
	(= device (:: device ds4_device_type))
	(= (: device$_data_buffer 4) 0)
	(= (: device$_data_buffer 5) 0)
	(= (: device$_data_buffer 6) 0)
	(= (: device$_data_buffer 7) 0)
	(= (: device$_data_buffer 8) 0)
	(= (: device$_data_buffer 9) 0)
	(= (: device$_data_buffer 10) 0)
	(<- file$write device$_file device$_data_buffer)
	(<- file$close device$_file)
))

(= list (... "ds4:device_list"))

(= poll (,,, device
	(? (! device) (@@ false))
	(= device (:: device ds4_device_type))
	(= data (<- file$read device$_file 64))
	(? (!== ($ data) 64) (@@ false))
	(= device$buttons 0)
	(= d-pad (& (: data 5) 15))
	(? (|| (<= d-pad 1) (=== d-pad 7)) (= device$buttons (| device$buttons BUTTON_UP)))
	(? (<= 3 d-pad 5) (= device$buttons (| device$buttons BUTTON_DOWN)))
	(? (<= 5 d-pad 7) (= device$buttons (| device$buttons BUTTON_LEFT)))
	(? (<= 1 d-pad 3) (= device$buttons (| device$buttons BUTTON_RIGHT)))
	(? (& (: data 5) 32) (= device$buttons (| device$buttons BUTTON_CROSS)))
	(? (& (: data 5) 64) (= device$buttons (| device$buttons BUTTON_CIRCLE)))
	(? (& (: data 5) 16) (= device$buttons (| device$buttons BUTTON_SQURARE)))
	(? (& (: data 5) 128) (= device$buttons (| device$buttons BUTTON_TRIANGLE)))
	(? (& (: data 6) 1) (= device$buttons (| device$buttons BUTTON_L1)))
	(? (& (: data 6) 2) (= device$buttons (| device$buttons BUTTON_R1)))
	(? (& (: data 6) 4) (= device$buttons (| device$buttons BUTTON_L2)))
	(? (& (: data 6) 8) (= device$buttons (| device$buttons BUTTON_R2)))
	(? (& (: data 6) 16) (= device$buttons (| device$buttons BUTTON_SHARE)))
	(? (& (: data 6) 32) (= device$buttons (| device$buttons BUTTON_OPTIONS)))
	(? (& (: data 6) 64) (= device$buttons (| device$buttons BUTTON_L3)))
	(? (& (: data 6) 128) (= device$buttons (| device$buttons BUTTON_R3)))
	(? (& (: data 7) 1) (= device$buttons (| device$buttons BUTTON_LOGO)))
	(? (& (: data 7) 2) (= device$buttons (| device$buttons BUTTON_TOUCHPAD)))
	(= device$left-x (- (: data 1) 128))
	(= device$left-y (- 127 (: data 2)))
	(= device$right-x (- (: data 3) 128))
	(= device$right-y (- 127 (: data 4)))
	(= device$left-trigger (: data 8))
	(= device$right-trigger (: data 9))
	(= device$battery (* (<< (: data 30) 5) (!== (& (: data 30) 15) 11)))
	(= device$acceleration$x (* (<- _decode_imu_data data 19) ACCELERATION_FACTOR))
	(= device$acceleration$y (* (<- _decode_imu_data data 21) ACCELERATION_FACTOR))
	(= device$acceleration$z (* (<- _decode_imu_data data 23) ACCELERATION_FACTOR))
	(= device$angular_velocity$x (- 0 (* (<- _decode_imu_data data 13) ANGULAR_VELOCITY_FACTOR)))
	(= device$angular_velocity$y (* (<- _decode_imu_data data 15) ANGULAR_VELOCITY_FACTOR))
	(= device$angular_velocity$z (* (<- _decode_imu_data data 17) ANGULAR_VELOCITY_FACTOR))
	(= device$touchpad [])
	(? (! (>> (: data 35) 7)) (<- array$push device$touchpad (<- _decode_touchpad_point data 35)))
	(? (! (>> (: data 39) 7)) (<- array$push device$touchpad (<- _decode_touchpad_point data 39)))
	(@@ true)
))

(= update (,,, device do_not_poll?
	(? (! device) (@@ false))
	(= device (:: device ds4_device_type))
	(= device$controls$red (& device$controls$red 255))
	(= device$controls$green (& device$controls$green 255))
	(= device$controls$blue (& device$controls$blue 255))
	(= (: device$_data_buffer 4) device$controls$small_rumble)
	(= (: device$_data_buffer 5) device$controls$big_rumble)
	(= (: device$_data_buffer 6) device$controls$red)
	(= (: device$_data_buffer 7) device$controls$green)
	(= (: device$_data_buffer 8) device$controls$blue)
	(= (: device$_data_buffer 9) device$controls$led_on)
	(= (: device$_data_buffer 10) device$controls$led_off)
	(? (!== (<- file$write device$_file device$_data_buffer) 32) {
		(:> "Device error!\n")
		(@@ false)
	})
	(@@ (|| do_not_poll? (<- poll device)))
))



(= ds4:device (. ds4_device_module_type
	ds4_device_controls_type
	ds4_device_touch_point_type
	ds4_device_type
	BATTERY_CHARGING
	BUTTON_CIRCLE
	BUTTON_CROSS
	BUTTON_DOWN
	BUTTON_L1
	BUTTON_L2
	BUTTON_L3
	BUTTON_LEFT
	BUTTON_LOGO
	BUTTON_OPTIONS
	BUTTON_R1
	BUTTON_R2
	BUTTON_R3
	BUTTON_RIGHT
	BUTTON_SHARE
	BUTTON_SQURARE
	BUTTON_TOUCHPAD
	BUTTON_TRIANGLE
	BUTTON_UP
	create
	list
	poll
	update
))
(## ds4:device)
