(---
	"array.sll"
	"audit.sll"
	"string.sll"
	"types.sll"
	"window_manager/_load_library.sll"
	"window_manager/geometry.sll"
)



(# FLAG_ACTIVE FLAG_MAIN_DISPLAY)



(= window_manager_display_type (&:
	(,,, data
		(= flags [])
		(? (& data$flags FLAG_ACTIVE) (<- array$push flags "Active"))
		(? (& data$flags FLAG_MAIN_DISPLAY) (<- array$push flags "Main"))
		(@@ (<- string$format "<window_manager:display geometry=%s flags=%s>"
			(:: data$geometry string_type)
			(<- string$join ", " flags)
		))
	) @@string@@
	(~ int_type) id
	(~ window_manager:geometry$window_manager_geometry_type) geometry
	(~ float_type) rotation
	(~ int_type) flags
))

(= window_manager_display_module_type (&:
	(~ type_type) window_manager_display_type
	(~ int_type) FLAG_ACTIVE
	(~ int_type) FLAG_MAIN_DISPLAY
	(~ func_type) enumerate
))



(= FLAG_ACTIVE 1)
(= FLAG_MAIN_DISPLAY 2)



(= enumerate (,,,
	(<- audit$audit "window.display.enumerate")
	(= data (<- (... "window:display_enumerate")))
	(@@ ([> (= i 0) (< i ($ data))
		(= display (: data i))
		(++ i)
		(:: display window_manager_display_type)
	))
))



(= window_manager:display (. window_manager_display_module_type
	window_manager_display_type
	FLAG_ACTIVE
	FLAG_MAIN_DISPLAY
	enumerate
))
(## window_manager:display)
