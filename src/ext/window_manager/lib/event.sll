(---
	"sys.sll"
	"thread.sll"
	"types.sll"
	"window_manager/_load_library.sll"
	"window_manager/cursor.sll"
	"window_manager/geometry.sll"
	"window_manager/util.sll"
	"window_manager/window.sll"
)



(= window_manager_native_error_type (?:
	(=== sys$PLATFORM "darwin") array_type
	(=== sys$PLATFORM "linux") (&:
		(~ int_type) error_code
		(~ int_type) resource_id
		(~ int_type) minor_code
		(~ int_type) major_code
	)
	array_type
))

(= window_manager_event_module_type (&:
	(~ type_type) window_manager_native_error_type
	(~ func_type) poll
))



(= _update_position (,,, window x y
	(? (&&
		(=== window$layout$geometry$x x)
		(=== window$layout$geometry$y y)
	) (@@))
	(= window$layout$geometry$x x)
	(= window$layout$geometry$y y)
	(<- window$callbacks$geometry window window$layout$geometry)
	(<- window$callbacks$position window x y)
))

(= _update_size (,,, window width height
	(? (&&
		(=== window$layout$geometry$width width)
		(=== window$layout$geometry$height height)
	) (@@))
	(= window$layout$geometry$width width)
	(= window$layout$geometry$height height)
	(<- window$callbacks$geometry window window$layout$geometry)
	(<- window$callbacks$size window width height)
	(<- window_manager:util$__gfx_callbacks$resize window)
))

(= poll (,,, blocking?
	(? (!.) {
		(:> "Event polling from non-main threads in not permitted!\n")
		(<- thread$exit 1)
	})
	(= data (<- (... "window:event_poll")
		(!! blocking?)
	))
	(-> (= i 0) (< i ($ data))
		(= event (: data i))
		(++ i)
		(= type (: event 0))
		(? (! type) {
			(<- window$callbacks$error (:: (: event 1) window_manager_native_error_type))
			(<<<)
		})
		(= window (<- window_manager:window$get (: event 1)))
		(? (! window) (<<<))
		(= event (>> event 2))
		(?? type
			1 {
				(= key (: event 0))
				(= state (: event 1))
				(= idx (>> key 6))
				(= mask (<< 1 (& key 63)))
				(= value (: window$inputs$_keys idx))
				(= (: window$inputs$_keys idx) (?: state
					(| value mask)
					(& value (~ mask))
				))
				(<- window$callbacks$key window key state)
				(<- (?: state
					window$callbacks$key_down
					window$callbacks$key_up
				) window key)
			}
			2 {
				(= button (: event 0))
				(= state (: event 1))
				(= mask (<< window_manager:window$FLAG_BUTTON_1 (- button 1)))
				(= window$flags (?: state
					(| window$flags mask)
					(& window$flags (~ mask))
				))
				(<- window$callbacks$button window button state)
				(<- (?: state
					window$callbacks$button_down
					window$callbacks$button_up
				) window button)
			}
			3 {
				(= mouse (:: event window_manager:cursor$window_manager_cursor_position_type))
				(= x_change (!== window$inputs$mouse$x mouse$x))
				(= y_change (!== window$inputs$mouse$y mouse$y))
				(? (|| x_change y_change) {
					(= window$inputs$mouse mouse)
					(<- window$callbacks$mouse window (: event 0) (: event 1))
					(? x_change (<- window$callbacks$mouse_x (: event 0)))
					(? y_change (<- window$callbacks$mouse_y (: event 1)))
				})
				(? (! (& window$flags window_manager:window$FLAG_HAS_MOUSE)) {
					(= window$flags (| window$flags window_manager:window$FLAG_HAS_MOUSE))
					(<- window$callbacks$mouse_state window true)
					(<- window$callbacks$mouse_enter window)
				})
			}
			4 {
				(= state (: event 0))
				(= window$flags (& window$flags (~ window_manager:window$FLAG_HAS_MOUSE)))
				(<- window$callbacks$mouse_state window false)
				(<- window$callbacks$mouse_leave window)
			}
			5 {
				(= state (: event 0))
				(= window$flags (?: state
					(| window$flags window_manager:window$FLAG_IN_FOCUS)
					(& window$flags (~ window_manager:window$FLAG_IN_FOCUS))
				))
				(<- window$callbacks$focus window state)
				(<- (?: state
					window$callbacks$focus_acquire
					window$callbacks$focus_lose
				) window)
			}
			6 {
				(<- _update_position window (: event 0) (: event 1))
				(<- _update_size window (: event 2) (: event 3))
			}
			7 {
				(= vertical? (: event 0))
				(<- (?: vertical?
					window$callbacks$scroll_y
					window$callbacks$scroll_x
				) (: event 1))
			}
			8 {
				(<- window$callbacks$redraw window (: event 0) (: event 1) (: event 2) (: event 3))
			}
			9 {
				(? (! (<- window$callbacks$ignore_close_request? window)) (<- window_manager:window$destroy window))
			}
			10 {
				(= window$layout$state (: event 0))
				(<- window$callbacks$state window (: event 0))
			}
			11 {
				(<- _update_position window (: event 0) (: event 1))
			}
			12 {
				(<- _update_size window (: event 0) (: event 1))
			}
		)
	)
))



(= window_manager:event (. window_manager_event_module_type
	window_manager_native_error_type
	poll
))
(## window_manager:event)
