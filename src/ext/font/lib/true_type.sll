(---
	"array.sll"
	"font/_load_library.sll"
	"gfx/buffer.sll"
	"gfx/context.sll"
	"gfx/data_type.sll"
	"gfx/sampler.sll"
	"gfx/texture.sll"
	"math.sll"
	"sort.sll"
	"string.sll"
	"types.sll"
)



(# create_font update_rendered_text update_rendered_text_font)



(= font_glyph_type (&:
	(,,, data
		(@@ (<- string$format "(%c: %d %d %u %u | +%u)"
			data$codepoint
			data$x
			data$y
			data$width
			data$height
			data$advance
		))
	) @@string@@
	char_type codepoint
	int_type x
	int_type y
	int_type width
	int_type height
	int_type advance
	string_type _data
	int_type _area
))

(= font_texture_glyph_type (&:
	float_type tx0
	float_type ty0
	float_type tx1
	float_type ty1
	int_type x
	int_type y
	int_type width
	int_type height
	int_type advance
))

(= font_atlas_type (&:
	(~ gfx:context$gfx_context_type) context
	(~ gfx:sampler$gfx_sampler_type) sampler
	gfx:texture$gfx_texture_type texture
	func_type callback
))

(= font_type (&:
	(~ int_type) ascent
	(~ int_type) descent
	(~ int_type) line_gap
	(~ array_type) glyphs
	int_type _total_area
))

(= font_texture_type (&:
	(~ font_type) font
	(~ array_type) glyphs
))

(= font_rendered_text_type (&:
	object_type font
	string_type text
	float_type z_coord
	float_type color_z_coord
	gfx:buffer$gfx_buffer_type vertex_buffer
	gfx:buffer$gfx_buffer_type index_buffer
))

(= font_true_type_module_type (&:
	(~ type_type) font_atlas_type
	(~ type_type) font_glyph_type
	(~ type_type) font_rendered_text_type
	(~ type_type) font_texture_type
	(~ type_type) font_type
	(~ func_type) add_font
	(~ func_type) create
	(~ func_type) create_atlas
	(~ func_type) create_font
	(~ func_type) create_rendered_text
	(~ func_type) draw_rendered_text
	(~ func_type) find
	(~ func_type) update_rendered_text
	(~ func_type) update_rendered_text_font
))



(= _copy_image (,,, src dst width height x_offset y_offset stride
	(= src_idx 0)
	(= dst_idx (+
		x_offset
		(* y_offset stride)
	))
	(= stride (- stride width))
	(-> (= y 0) (< y height)
		(-> (= x 0) (< x width)
			(= (: dst dst_idx) (: src src_idx))
			(++ x)
			(++ src_idx)
			(++ dst_idx)
		)
		(= dst_idx (+ dst_idx stride))
		(++ y)
	)
))

(= add_font (,,, atlas font
	(? (! atlas) (@@))
	(= atlas (:: atlas font_atlas_type))
	(= font (:: font font_type))
	(= glyphs (<- sort$sort font$glyphs true false (,,, glyph
		(@@ [glyph$height glyph$_area])
	)))
	(= texture_glyphs (<- array$create 256))
	(= first_glyph (: glyphs 0))
	(= row_height first_glyph$height)
	(= width (<- math$int_sqrt font$_total_area))
	(= texture_data (* "\x00" width row_height))
	(= x_offset 0)
	(= y_offset 0)
	(-> (= i 0) (< i ($ glyphs))
		(= glyph (: glyphs i))
		(? (< (- width x_offset) glyph$width) {
			(= x_offset 0)
			(= y_offset (+ y_offset row_height))
			(= row_height glyph$height)
			(= texture_data (+
				texture_data
				(* "\x00" width row_height)
			))
		})
		(<- _copy_image glyph$_data texture_data glyph$width glyph$height x_offset y_offset width)
		(= (: texture_glyphs (:: glyph$codepoint int_type)) (. font_texture_glyph_type
			x_offset
			y_offset
			(+ x_offset glyph$width)
			(+ y_offset glyph$height)
			glyph$x
			glyph$y
			glyph$width
			glyph$height
			glyph$advance
		))
		(= x_offset (+ x_offset glyph$width))
		(++ i)
	)
	(= height (+ y_offset row_height))
	(= inv_width (/ 1.0 width))
	(= inv_height (/ 1.0 height))
	(-> (= i 0) (< i 256)
		(= glyph (: texture_glyphs i))
		(++ i)
		(? (! glyph) (<<<))
		(= glyph$tx0 (* glyph$tx0 inv_width))
		(= glyph$ty0 (* glyph$ty0 inv_height))
		(= glyph$tx1 (* glyph$tx1 inv_width))
		(= glyph$ty1 (* glyph$ty1 inv_height))
	)
	(= atlas$texture (<- gfx:texture$create atlas$context [width height] (| gfx:data_type$SIZE_8BIT gfx:data_type$COUNT_1 gfx:data_type$TYPE_COLOR_UNORM) (<- gfx:buffer$create atlas$context gfx:buffer$TYPE_TEXTURE gfx:buffer$DATA_TYPE_UINT8 (:: texture_data array_type) true)))
	;;; FIXME: Currently due to an unknown bug the following line needs to be duplicated
	(<- atlas$callback atlas)
	(<- atlas$callback atlas)
	(@@ (. font_texture_type
		font
		texture_glyphs
	))
))

(= create (,,, data
	(@@ (<-* create_font (<- (... "font:true_type_create")
		(:: data string_type)
	)))
))

(= create_atlas (,,, ctx callback
	(? (! ctx) (@@ nil))
	(= ctx (:: ctx gfx:context$gfx_context_type))
	(= callback (:: callback func_type))
	(@@ (. font_atlas_type
		ctx
		(<- gfx:sampler$create ctx (. gfx:sampler$gfx_sampler_config_type
			gfx:sampler$FILTER_LINEAR
			gfx:sampler$FILTER_LINEAR
			gfx:sampler$ADDRESS_MODE_TYPE_CLAMP
			gfx:sampler$ADDRESS_MODE_TYPE_CLAMP
			gfx:sampler$ADDRESS_MODE_TYPE_CLAMP
		))
		(<- gfx:texture$create ctx [1 1] (| gfx:data_type$SIZE_8BIT gfx:data_type$COUNT_1 gfx:data_type$TYPE_COLOR_UNORM) (<- gfx:buffer$create ctx gfx:buffer$TYPE_TEXTURE gfx:buffer$DATA_TYPE_UINT8 [0xff] true))
		callback
	))
))

(= create_font (,,, ascent descent line_gap glyphs
	(= glyphs (:: glyphs array_type))
	(= area 0)
	(= out (. font_type
		ascent
		descent
		line_gap
		([> (= i 0) (< i ($ glyphs))
			(= glyph (:: (: glyphs i) font_glyph_type))
			(++ i)
			(= glyph$_area (* glyph$width glyph$height))
			(= area (+ area glyph$_area))
			glyph
		)
	))
	(= out$_total_area area)
	(@@ out)
))

(= create_rendered_text (,,, ctx font text
	(? (! ctx) (@@ nil))
	(= ctx (:: ctx gfx:context$gfx_context_type))
	(= vertex_buffer (<- gfx:buffer$create ctx gfx:buffer$TYPE_VERTEX gfx:buffer$DATA_TYPE_FLOAT32))
	(<- gfx:buffer$hint_update_frequency vertex_buffer gfx:buffer$FREQUENCY_MEDIUM)
	(= index_buffer (<- gfx:buffer$create ctx gfx:buffer$TYPE_INDEX gfx:buffer$DATA_TYPE_UINT16))
	(<- gfx:buffer$hint_update_frequency index_buffer gfx:buffer$FREQUENCY_MEDIUM)
	(= out (. font_rendered_text_type
		nil
		""
		0.0
		0.0
		vertex_buffer
		index_buffer
	))
	(? font (<- update_rendered_text_font out font))
	(? text (<- update_rendered_text out text))
	(@@ out)
))

(= draw_rendered_text (,,, rendered_text
	(? (! rendered_text) (@@))
	(= rendered_text (:: rendered_text font_rendered_text_type))
	(<- gfx:buffer$use rendered_text$vertex_buffer)
	(<- gfx:buffer$use rendered_text$index_buffer)
	(<- gfx:buffer$draw rendered_text$index_buffer)
))

(= find (,,, @@names@@
	(-> (= i 0) (< i ($ @@names@@))
		(= out (<- (... "font:true_type_find")
			(:: (: @@names@@ i) string_type)
		))
		(? out (@@ out))
		(++ i)
	)
	(@@ nil)
))

(= update_rendered_text (,,, rendered_text text
	(? (! rendered_text) (@@))
	(= rendered_text (:: rendered_text font_rendered_text_type))
	(= text (:: text string_type))
	(= rendered_text$text text)
	(= rendered_text$vertex_buffer$data (<- array$create (* ($ text) 24)))
	(= rendered_text$index_buffer$data (<- array$create (* ($ text) 6)))
	(= scale 0.5)
	(= x 0.0)
	(= y (* rendered_text$font$font$ascent scale))
	(= z rendered_text$z_coord)
	(= tz rendered_text$color_z_coord)
	(-> (= i 0) (< i ($ text))
		(= glyph (: rendered_text$font$glyphs (:: (: text i) int_type)))
		(? glyph {
			(= x0 (+ x (* glyph$x scale)))
			(= y0 (+ y (* glyph$y scale)))
			(= x1 (+ x0 (* glyph$width scale)))
			(= y1 (+ y0 (* glyph$height scale)))
			(= tx0 glyph$tx0)
			(= ty0 glyph$ty0)
			(= tx1 glyph$tx1)
			(= ty1 glyph$ty1)
			(= j (* i 24))
			(= (: rendered_text$vertex_buffer$data j) x0)
			(= (: rendered_text$vertex_buffer$data (+ j 1)) y0)
			(= (: rendered_text$vertex_buffer$data (+ j 2)) z)
			(= (: rendered_text$vertex_buffer$data (+ j 3)) tx0)
			(= (: rendered_text$vertex_buffer$data (+ j 4)) ty0)
			(= (: rendered_text$vertex_buffer$data (+ j 5)) tz)
			(= (: rendered_text$vertex_buffer$data (+ j 6)) x1)
			(= (: rendered_text$vertex_buffer$data (+ j 7)) y0)
			(= (: rendered_text$vertex_buffer$data (+ j 8)) z)
			(= (: rendered_text$vertex_buffer$data (+ j 9)) tx1)
			(= (: rendered_text$vertex_buffer$data (+ j 10)) ty0)
			(= (: rendered_text$vertex_buffer$data (+ j 11)) tz)
			(= (: rendered_text$vertex_buffer$data (+ j 12)) x1)
			(= (: rendered_text$vertex_buffer$data (+ j 13)) y1)
			(= (: rendered_text$vertex_buffer$data (+ j 14)) z)
			(= (: rendered_text$vertex_buffer$data (+ j 15)) tx1)
			(= (: rendered_text$vertex_buffer$data (+ j 16)) ty1)
			(= (: rendered_text$vertex_buffer$data (+ j 17)) tz)
			(= (: rendered_text$vertex_buffer$data (+ j 18)) x0)
			(= (: rendered_text$vertex_buffer$data (+ j 19)) y1)
			(= (: rendered_text$vertex_buffer$data (+ j 20)) z)
			(= (: rendered_text$vertex_buffer$data (+ j 21)) tx0)
			(= (: rendered_text$vertex_buffer$data (+ j 22)) ty1)
			(= (: rendered_text$vertex_buffer$data (+ j 23)) tz)
			(= x (+ x (* glyph$advance scale)))
		})
		(= j (* i 6))
		(= k (<< i 2))
		(= (: rendered_text$index_buffer$data j) (+ k 2))
		(= (: rendered_text$index_buffer$data (+ j 1)) (+ k 1))
		(= (: rendered_text$index_buffer$data (+ j 2)) k)
		(= (: rendered_text$index_buffer$data (+ j 3)) (+ k 3))
		(= (: rendered_text$index_buffer$data (+ j 4)) (+ k 2))
		(= (: rendered_text$index_buffer$data (+ j 5)) k)
		(++ i)
	)
	(<- gfx:buffer$sync rendered_text$vertex_buffer)
	(<- gfx:buffer$sync rendered_text$index_buffer)
))

(= update_rendered_text_z_coords (,,, rendered_text
	(? (! rendered_text) (@@))
	(= rendered_text (:: rendered_text font_rendered_text_type))
))

(= update_rendered_text_font (,,, rendered_text font
	(? (! rendered_text) (@@))
	(= rendered_text (:: rendered_text font_rendered_text_type))
	(= font (:: font font_texture_type))
	(= rendered_text$font font)
))



(= font:true_type (. font_true_type_module_type
	font_atlas_type
	font_glyph_type
	font_rendered_text_type
	font_texture_type
	font_type
	add_font
	create
	create_atlas
	create_font
	create_rendered_text
	draw_rendered_text
	find
	update_rendered_text
	update_rendered_text_font
))
(## font:true_type)
