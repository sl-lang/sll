(---
	"font/_load_library.sll"
	"gfx/sampler.sll"
	"gfx/texture.sll"
	"types.sll"
)



(= font_glyph_type (&:
	float_type tx0
	float_type ty0
	float_type tx1
	float_type ty1
	float_type gx0
	float_type gy0
	float_type gx1
	float_type gy1
	float_type advance
))

(= font_type (&:
	(~ gfx:sampler$gfx_sampler_type) sampler
	(~ gfx:texture$gfx_texture_type) texture
	(~ int_type) width
	(~ int_type) height
	(~ array_type) glypths
	(~ int_type) ascent
	(~ int_type) descent
	(~ int_type) line_gap
))

(= font_true_type_module_type (&:
	(~ type_type) font_glyph_type
	(~ type_type) font_type
	(~ func_type) create
	(~ func_type) find
))



(= create (,,, data
	(= out (<- (... "font:true_type_create")
		(:: data string_type)
	))
	(= inv_width (/ 1 out$width))
	(= inv_height (/ 1 out$height))
	(= glypths (: out 3))
	(@@ (. font_type
		nil
		nil
		(: out 0)
		(: out 1)
		([> (= i 0) (< i ($ glypths))
			(= glypth (:: (: glypths i) font_glyph_type))
			(= glypth$tx0 (* glypth$tx0 inv_width))
			(= glypth$ty0 (* glypth$ty0 inv_height))
			(= glypth$tx1 (* glypth$tx1 inv_width))
			(= glypth$ty1 (* glypth$ty1 inv_height))
			(++ i)
			glypth
		)
		(: out 4)
		(: out 5)
		(: out 6)
	))
))

(= find (,,, @@names@@
	(-> (= i 0) (< i ($ @@names@@))
		(= out (<- (... "font:true_type_find")
			(:: (: @@names@@ i) string_type)
		))
		(? out (@@ out))
		(++ i)
	)
	(@@ nil)
))



(= font:true_type (. font_true_type_module_type
	font_glyph_type
	font_type
	create
	find
))
(## font:true_type)
