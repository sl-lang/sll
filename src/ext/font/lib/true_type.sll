(---
	"file.sll"
	"font/_load_library.sll"
	"path.sll"
	"string.sll"
	"types.sll"
)



(# create_font)



(= font_glyph_type (&:
	(,,, data
		(@@ (<- string$format "(%c: %f %f %f %f | %+f)"
			data$codepoint
			data$x
			data$y
			data$width
			data$height
			data$advance
		))
	) @@string@@
	char_type codepoint
	float_type x
	float_type y
	float_type width
	float_type height
	float_type advance
	string_type _data
	int_type _data_width
	float_type _area
))

(= font_type (&:
	(~ float_type) ascent
	(~ float_type) descent
	(~ float_type) line_gap
	(~ array_type) glyphs
	float_type _total_area
))

(= font_true_type_module_type (&:
	(~ type_type) font_glyph_type
	(~ type_type) font_type
	(~ func_type) create
	(~ func_type) create_font
	(~ func_type) find
	(~ func_type) load
))



(= _file_path_to_font <>)



(= create (,,, data
	(@@ (<-* create_font (<- (... "font:true_type_create")
		(:: data string_type)
	)))
))

(= create_font (,,, ascent descent line_gap glyphs
	(= glyphs (:: glyphs array_type))
	(= area 0)
	(= out (. font_type
		ascent
		descent
		line_gap
		([> (= i 0) (< i ($ glyphs))
			(= glyph (:: (: glyphs i) font_glyph_type))
			(++ i)
			(= glyph$_area (* glyph$width glyph$height))
			(= area (+ area glyph$_area))
			glyph
		)
	))
	(= out$_total_area area)
	(@@ out)
))

(= find (,,, @@names@@
	(-> (= i 0) (< i ($ @@names@@))
		(= out (<- (... "font:true_type_find")
			(:: (: @@names@@ i) string_type)
		))
		(? out (@@ out))
		(++ i)
	)
	(@@ nil)
))

(= load (,,, file_path
	(? (! file_path) (@@ nil))
	(= file_path (:: file_path string_type))
	(= file_path (<- path$absolute file_path))
	(? (: _file_path_to_font file_path) (@@ (: _file_path_to_font file_path)))
	(= handle (<- file$open file_path "r"))
	(? (!== (:? handle) file$file_type) (@@ handle))
	(= out (<- create (<- file$read handle)))
	(= (: _file_path_to_font file_path) out)
	(@@ out)
))



(= font:true_type (. font_true_type_module_type
	font_glyph_type
	font_type
	create
	create_font
	find
	load
))
(## font:true_type)
